Using System
Using System.Collections
Using System.ComponentModel
Using System.Data
Using System.Drawing
Using System.Text
Using System.Windows.Forms

Using Disney.SRR

BegClass LeversForm1 Extends(System.Windows.Forms.Form) Access(*Public)

	/Region Declarations
	DclFld statusStrip1 System.Windows.Forms.StatusStrip Access(*Private) WithEvents(*Yes)
	DclFld components Type(System.ComponentModel.IContainer) Inz(*Nothing)
	DclFld label3 System.Windows.Forms.Label Access(*Private) WithEvents(*Yes)
	DclFld label1 System.Windows.Forms.Label Access(*Private) WithEvents(*Yes)
	DclFld label2 System.Windows.Forms.Label Access(*Private) WithEvents(*Yes)
	DclFld cbxDept System.Windows.Forms.ComboBox Access(*Private) WithEvents(*Yes)
	DclFld txtUser System.Windows.Forms.TextBox Access(*Private) WithEvents(*Yes)
	DclFld groupBox1 System.Windows.Forms.GroupBox Access(*Private) WithEvents(*Yes)
	DclFld rbWeekly System.Windows.Forms.RadioButton Access(*Private) WithEvents(*Yes)
	DclFld rbDaily System.Windows.Forms.RadioButton Access(*Private) WithEvents(*Yes)
	DclFld btnCancel System.Windows.Forms.Button Access(*Private) WithEvents(*Yes)
	DclFld txtFileGroup System.Windows.Forms.TextBox Access(*Private) WithEvents(*Yes)
	DclFld btnContinue System.Windows.Forms.Button Access(*Private) WithEvents(*Yes)
	DclFld errorProvider1 System.Windows.Forms.ErrorProvider Access(*Private) WithEvents(*Yes)
	DclFld StatusMsg System.Windows.Forms.ToolStripStatusLabel Access(*Private) WithEvents(*Yes)
	
	Dclfld DetailForm   Type(Disney.SRR.LeversForm2)
	DclFld FrmMbr       Type(*Char)  Len(10)
	DclFld ToMbr        Type(*Char)  Len(10)
	DclFld Department   Type(*Zoned) Len(3,0)
	DclFld AS400Profile Type(*Char)  Len(10)
	DclFld LockStatus   Type(*String)
	
	DclFld	 Name(_NL)	Type(*String)		// Initialized in constructor to environment.newline	

	DclDB Name(SRR) DBName("TDST")
	
	DclDiskFile Name(DSSRLCK) Type(*Update) Org(*Indexed) +
		DB(SRR) File('*Libl/DSSRLCK') AddRec(*yes) ImpOpen(*No) +
		RnmFmt(RDSSRLCK, UDSSRLCK)
		
	DclDiskFile Name(DSSRLCKL1) Type(*Update) Org(*Indexed) +
		DB(SRR) File('*Libl/DSSRLCKL1') ImpOpen(*No)

	DclDiskFile Name(DSUSRDA) Type(*Input) Org(*Indexed) +
		DB(SRR) File('*Libl/DSUSRDA') ImpOpen(*No)
	
	DclDiskFile Name(IPUSERS) Type(*Input) Org(*Indexed) +
		DB(SRR) File('*Libl/IPUSERS') ImpOpen(*No)
		
	DclDiskFile Name(IPDEPTS) Type(*Input) Org(*Indexed) +
		DB(SRR) File('*Libl/IPDEPTS') ImpOpen(*No)

	DclPlist  Name( CPYMBRParms )
		DclParm FrmMbr      DBDirection( *Input )		// Only to iSeries
		DclParm ToMbr       DBDirection( *Input )		// Only to iSeries
		DclParm Dept   Type(*Packed) Len(3,0) DBDirection( *Input )		// Only to iSeries
	/EndRegion

	BegConstructor Access(*Public)
		DclSrParm SRR Type(ASNA.VisualRPG.Runtime.Database)
			
		InitializeComponent()
		
//		If Not SRR.IsOpen			// Remove in release build
//			Connect SRR				// Remove in release build
//		EndIf						// Remove in release build

		*This.SRR = SRR
		
		AS400Profile = SRR.User.ToUpper()
		
		If Not DSSRLCK.IsOpen
			Open DSSRLCK
		EndIf
		
		If Not DSSRLCKl1.IsOpen
			Open DSSRLCKL1
		EndIf
		
		If Not DSUSRDA.IsOpen
			Open DSUSRDA
		EndIf

		If Not IPUSERS.IsOpen
			Open IPUSERS
		EndIf
	
		If Not IPDEPTS.IsOpen
			Open IPDEPTS
		EndIf
		
		_NL	= Environment.NewLine
	EndConstructor

	/region Windows Form Designer generated code
    
	// Required method for Designer support - do not modify
	// the contents of this method with the code editor.
	BegSr InitializeComponent
		*this.components = *new System.ComponentModel.Container()
		*this.label3 = *new System.Windows.Forms.Label()
		*this.statusStrip1 = *new System.Windows.Forms.StatusStrip()
		*this.StatusMsg = *new System.Windows.Forms.ToolStripStatusLabel()
		*this.cbxDept = *new System.Windows.Forms.ComboBox()
		*this.label1 = *new System.Windows.Forms.Label()
		*this.label2 = *new System.Windows.Forms.Label()
		*this.txtUser = *new System.Windows.Forms.TextBox()
		*this.txtFileGroup = *new System.Windows.Forms.TextBox()
		*this.btnContinue = *new System.Windows.Forms.Button()
		*this.btnCancel = *new System.Windows.Forms.Button()
		*this.groupBox1 = *new System.Windows.Forms.GroupBox()
		*this.rbWeekly = *new System.Windows.Forms.RadioButton()
		*this.rbDaily = *new System.Windows.Forms.RadioButton()
		*this.errorProvider1 = *new System.Windows.Forms.ErrorProvider(*this.components)
		*this.statusStrip1.SuspendLayout()
		*this.groupBox1.SuspendLayout()
		(*this.errorProvider1 *As System.ComponentModel.ISupportInitialize).BeginInit()
		*this.SuspendLayout()
		//
		//label3
		//
		*this.label3.AutoSize = *True
		*this.label3.Location = *new System.Drawing.Point(13, 60)
		*this.label3.Margin = *new System.Windows.Forms.Padding(4, 0, 4, 0)
		*this.label3.Name = "label3"
		*this.label3.Size = *new System.Drawing.Size(92, 19)
		*this.label3.TabIndex = 0
		*this.label3.Text = "Department"
		//
		//statusStrip1
		//
		*this.statusStrip1.Items.AddRange(*new System.Windows.Forms.ToolStripItem[] {*this.StatusMsg})
		*this.statusStrip1.Location = *new System.Drawing.Point(0, 299)
		*this.statusStrip1.Name = "statusStrip1"
		*this.statusStrip1.Padding = *new System.Windows.Forms.Padding(2, 0, 21, 0)
		*this.statusStrip1.Size = *new System.Drawing.Size(639, 22)
		*this.statusStrip1.TabIndex = 9
		*this.statusStrip1.Text = "Processing..."
		//
		//StatusMsg
		//
		*this.StatusMsg.Name = "StatusMsg"
		*this.StatusMsg.Size = *new System.Drawing.Size(64, 17)
		*this.StatusMsg.Text = "LeverForm1"
		//
		//cbxDept
		//
		*this.cbxDept.FormattingEnabled = *True
		*this.cbxDept.Location = *new System.Drawing.Point(161, 57)
		*this.cbxDept.Margin = *new System.Windows.Forms.Padding(4)
		*this.cbxDept.Name = "cbxDept"
		*this.cbxDept.Size = *new System.Drawing.Size(367, 27)
		*this.cbxDept.TabIndex = 2
		//
		//label1
		//
		*this.label1.AutoSize = *True
		*this.label1.Location = *new System.Drawing.Point(13, 13)
		*this.label1.Name = "label1"
		*this.label1.Size = *new System.Drawing.Size(70, 19)
		*this.label1.TabIndex = 33
		*this.label1.Text = "You are:"
		//
		//label2
		//
		*this.label2.AutoSize = *True
		*this.label2.Location = *new System.Drawing.Point(340, 13)
		*this.label2.Name = "label2"
		*this.label2.Size = *new System.Drawing.Size(126, 19)
		*this.label2.TabIndex = 34
		*this.label2.Text = "Using Filegroup:"
		//
		//txtUser
		//
		*this.txtUser.Enabled = *False
		*this.txtUser.Location = *new System.Drawing.Point(161, 10)
		*this.txtUser.Name = "txtUser"
		*this.txtUser.Size = *new System.Drawing.Size(130, 27)
		*this.txtUser.TabIndex = 0
		//
		//txtFileGroup
		//
		*this.txtFileGroup.Location = *new System.Drawing.Point(488, 10)
		*this.txtFileGroup.MaxLength = 3
		*this.txtFileGroup.Name = "txtFileGroup"
		*this.txtFileGroup.Size = *new System.Drawing.Size(39, 27)
		*this.txtFileGroup.TabIndex = 1
		//
		//btnContinue
		//
		*this.btnContinue.Anchor = ((System.Windows.Forms.AnchorStyles.Bottom *BitOr System.Windows.Forms.AnchorStyles.Right) *As System.Windows.Forms.AnchorStyles)
		*this.btnContinue.Location = *new System.Drawing.Point(290, 197)
		*this.btnContinue.Name = "btnContinue"
		*this.btnContinue.Size = *new System.Drawing.Size(100, 41)
		*this.btnContinue.TabIndex = 37
		*this.btnContinue.Text = "Continue"
		*this.btnContinue.UseVisualStyleBackColor = *True
		//
		//btnCancel
		//
		*this.btnCancel.Anchor = ((System.Windows.Forms.AnchorStyles.Bottom *BitOr System.Windows.Forms.AnchorStyles.Right) *As System.Windows.Forms.AnchorStyles)
		*this.btnCancel.DialogResult = System.Windows.Forms.DialogResult.Cancel
		*this.btnCancel.Location = *new System.Drawing.Point(427, 197)
		*this.btnCancel.Name = "btnCancel"
		*this.btnCancel.Size = *new System.Drawing.Size(100, 41)
		*this.btnCancel.TabIndex = 38
		*this.btnCancel.Text = "Cancel"
		*this.btnCancel.UseVisualStyleBackColor = *True
		//
		//groupBox1
		//
		*this.groupBox1.Controls.Add(*this.rbWeekly)
		*this.groupBox1.Controls.Add(*this.rbDaily)
		*this.groupBox1.Location = *new System.Drawing.Point(17, 110)
		*this.groupBox1.Name = "groupBox1"
		*this.groupBox1.Size = *new System.Drawing.Size(218, 128)
		*this.groupBox1.TabIndex = 39
		*this.groupBox1.TabStop = *False
		*this.groupBox1.Text = "Changes"
		//
		//rbWeekly
		//
		*this.rbWeekly.AutoSize = *True
		*this.rbWeekly.Location = *new System.Drawing.Point(23, 78)
		*this.rbWeekly.Name = "rbWeekly"
		*this.rbWeekly.Size = *new System.Drawing.Size(77, 23)
		*this.rbWeekly.TabIndex = 1
		*this.rbWeekly.Text = "Weekly"
		*this.rbWeekly.TextAlign = System.Drawing.ContentAlignment.MiddleCenter
		*this.rbWeekly.UseVisualStyleBackColor = *True
		//
		//rbDaily
		//
		*this.rbDaily.AutoSize = *True
		*this.rbDaily.Checked = *True
		*this.rbDaily.Location = *new System.Drawing.Point(23, 35)
		*this.rbDaily.Name = "rbDaily"
		*this.rbDaily.Size = *new System.Drawing.Size(62, 23)
		*this.rbDaily.TabIndex = 0
		*this.rbDaily.TabStop = *True
		*this.rbDaily.Text = "Daily"
		*this.rbDaily.UseVisualStyleBackColor = *True
		//
		//errorProvider1
		//
		*this.errorProvider1.BlinkStyle = System.Windows.Forms.ErrorBlinkStyle.NeverBlink
		*this.errorProvider1.ContainerControl = *this
		//
		//LeversForm1
		//
		*this.AcceptButton = *this.btnContinue
		*this.AutoScaleDimensions = *new System.Drawing.SizeF(9, 19)
		*this.AutoScaleMode = System.Windows.Forms.AutoScaleMode.Font
		*this.BackColor = System.Drawing.SystemColors.Control
		*this.CancelButton = *this.btnCancel
		*this.ClientSize = *new System.Drawing.Size(639, 321)
		*this.Controls.Add(*this.groupBox1)
		*this.Controls.Add(*this.btnCancel)
		*this.Controls.Add(*this.btnContinue)
		*this.Controls.Add(*this.txtFileGroup)
		*this.Controls.Add(*this.txtUser)
		*this.Controls.Add(*this.label2)
		*this.Controls.Add(*this.label1)
		*this.Controls.Add(*this.cbxDept)
		*this.Controls.Add(*this.statusStrip1)
		*this.Controls.Add(*this.label3)
		*this.Font = *new System.Drawing.Font("Tahoma", 12, System.Drawing.FontStyle.Regular, System.Drawing.GraphicsUnit.Point, (0 *As System.Byte))
		*this.ForeColor = System.Drawing.SystemColors.ControlText
		*this.Margin = *new System.Windows.Forms.Padding(4)
		*this.MaximizeBox = *False
		*this.Name = "LeversForm1"
		*this.ShowIcon = *False
		*this.ShowInTaskbar = *False
		*this.StartPosition = System.Windows.Forms.FormStartPosition.CenterScreen
		*this.Text = "Summary Allocators Workbench"
		*this.statusStrip1.ResumeLayout(*False)
		*this.statusStrip1.PerformLayout()
		*this.groupBox1.ResumeLayout(*False)
		*this.groupBox1.PerformLayout()
		(*this.errorProvider1 *As System.ComponentModel.ISupportInitialize).EndInit()
		*this.ResumeLayout(*False)
		*this.PerformLayout()
	EndSr

	/endregion

	BegSr btnContinue_Click Access(*Private) Event(*this.btnContinue.Click)
		DclSrParm sender *Object
		DclSrParm e System.EventArgs
		
		DclFld FileGroup      Type(*Packed) Len(3,0)
		DclFld SelectedDept   Type(*Char)   Len(3)
		DclFld DepartmentText Type(*String)
		DclFld DialogReturn	  Type(system.Windows.Forms.DialogResult)
		DclFld Lck_FileGroup  Type(*Char)   Len(3)
		
		SetMousePtr Icon(*HourGlass)

		// Validate file group value
		errorProvider1.SetError( txtFileGroup, String.Empty)
		
		Try
			FileGroup = Convert.toInt32(txtFileGroup.Text)
		Catch Err Exception
			errorProvider1.SetError(txtFileGroup, "Enter a valid number")
			LeaveSR
		EndTry
		
		If FileGroup = *Zero
			errorProvider1.SetError(txtFileGroup, "Please enter a File Group")
			LeaveSr
		EndIf
		
		If FileGroup < UFGP15 or FileGroup > UTGP15
			errorProvider1.SetError(txtFileGroup, "The Entered File Group is not within your range")
			LeaveSr
		EndIf
		
		
		// Validating Department Selection
		errorProvider1.SetError( cbxDept, String.Empty)
		
		DepartmentText = cbxDept.Text.Trim()
		If DepartmentText = String.Empty
			errorProvider1.SetError( cbxDept, "Please enter a Department Number")
			LeaveSR
		EndIf
		
		Try
			SelectedDept = %SubSt(DepartmentText,1,3)
		Catch Err Exception
			errorProvider1.SetError( cbxDept, "Please enter a three digit number")
			LeaveSr
		EndTry
		
		If SelectedDept = String.Empty
			errorProvider1.SetError( cbxDept, "Please enter a Department Number")
			LeaveSr
		EndIf
		
		Try
			Department = Convert.ToInt32(SelectedDept)
		Catch Err Exception
			errorProvider1.SetError( cbxDept, "Department Number is invalid, please re-enter")
			LeaveSR
		EndTry
		

		// Check against department file 
		Department = Convert.ToInt32(SelectedDept)
		Chain DSUSRDA Key(AS400Profile, Department)
		If DSUSRDA.IsFound
			cbxDept.Text = USDEPT.ToString('000')
			Chain IPDEPTS Key(USDEPT)
			If IPDEPTS.IsFound
				DepartmentText = %Trim(cbxDept.Text + ' ' + DNAM)
				cbxDept.Text = DepartmentText
			EndIf
		Else
			errorProvider1.SetError( cbxDept, "You have selected an Invalid Department")
			LeaveSr
		EndIf				
		
		
		// Construct From and To Member Names
		If rbDaily.Checked
			FrmMbr = 'DLOAD'
			ToMbr = 'F' + FileGroup.ToString('000')
		Else
			FrmMbr = 'WELOAD'
			ToMbr = 'DPT' + Department.ToString('000') + FileGroup.ToString('000')
		EndIf
		
		
		// Lock Department/File Group
		StatusMsg.Text = 'Locking Selected Department...'
		DoEvents
		
		// Check for lock on file group
		SetLL DSSRLCKL1 Key(ToMbr)
		If DSSRLCKL1.IsEqual
			errorProvider1.SetError( txtFileGroup, "File Group is in use, try again later")
			LeaveSR
		EndIf
		
		SetLL DSSRLCK key(12, Department)     
		DoUntil DSSRLCK.IsEof
			ReadE DSSRLCK Key(12, Department) Access(*NoLock)			// Update capable file
			If not %eof
				If %SubSt(LoMbr,1,3) = 'DPT'							// Weekly member
					Lck_FileGroup = %SubSt(LoMbr,7,3)
				Else													// Daily member
					Lck_FileGroup = %SubSt(LoMbr,2,3)
				EndIf
				If Lck_FileGroup = FileGroup.ToString('000')
					errorProvider1.SetError( cbxDept, "Department is in use, try again later" )
					LeaveSR
				EndIf
			EndIf
		EndDo
		
		// Must not share the same Department in either Daily or Weekly
		SetLL DSSRLCK key(12, Department)
		DoUntil DSSRLCK.IsEof
			ReadE DSSRLCK Key(12, Department) Access(*NoLock)			// Update capable file
			If not %eof
				If (LoTyp = 'Daily' And FrmMbr = 'DLOAD') Or (LoTyp = 'Weekly' And FrmMbr = 'WELOAD')
					errorProvider1.SetError( cbxDept, "Department is in use, try again later" )
					LeaveSR
				EndIf
			EndIf
		EndDo

		
		// Create Lock record
		LockStatus = LockDepartment()
		If LockStatus = 'Error'
			MsgBox 'An error occured while trying to Lock the Department, please try again' +
				Buttons(*Ok) Icon(*Exclamation)
			LeaveSR
		EndIf
		
		// Creating working members in files
		StatusMsg.Text = 'Creating File members. Please wait...'
		DoEvents
		Dept = Department
		Try
			Call Pgm( "*Libl/DS882SC") ParmList( CpyMbrParms ) DB( SRR )
		Catch Err Exception
			MsgBox 'Unable to copy members. Contact Support'
			LeaveSR
		EndTry
		
		
		// Create Second Form instance passing parms to Constructor
		StatusMsg.Text = 'Creating Department grid, Please wait...'
		DoEvents
		
		// Instance the Grid form
		If (DetailForm = *Nothing) or (DetailForm.Isdisposed)
			DetailForm = *New LeversForm2(SRR, ToMbr, Department, FileGroup, DepartmentText, + 
					AS400profile, FrmMbr, LODAT)
		Endif	
		
		// Show Modal
		*this.SuspendLayout()

		DialogReturn = DetailForm.ShowDialog()
		
		If DialogReturn = DialogResult.Cancel
			Delete DSSRLCKL1 Key(ToMbr)
		EndIf

		DetailForm = *Nothing
		
		SetMousePtr Icon(*Dft)
		*this.ResumeLayout()
	EndSr
	
	BegSr Form_Load Access(*Private) Event(*this.Load)
		DclSrParm sender Type(*Object)
		DclSrParm e Type(System.EventArgs)
		
		DclFld DeptCode Type(*String)
		
		*this.SuspendLayout()
		
		txtUser.Text = AS400Profile		// Display profile on screen
		
		// Set default File Group
		Chain DSUSRDA Key(AS400Profile)
		If DSUSRDA.IsFound
			Chain IPUSERS Key(USIPPR)
			If IPUSERS.IsFound
				txtFileGroup.Text = UFGD.ToString('000')
			EndIf
		EndIf
		
		// Load Departments
		cbxDept.Items.Clear()
		SetRange DSUSRDA FirstKey(AS400Profile)
		DoUntil DSUSRDA.IsEof
			Read DSUSRDA
			If Not DSUSRDA.IsEof
				DeptCode = USDEPT.ToString('000')
				Chain IPDEPTS Key(USDEPT)
				If IPDEPTS.IsFound
					DeptCode = %Trim(DeptCode + ' ' + DNAM)
				EndIf
				cbxDept.Items.Add(DeptCode)
			EndIf
		EndDo
		
		*this.ResumeLayout()
	EndSr
	
	BegSr SignOn_FormClosing Access(*Private) Event(*this.FormClosing)
		DclSrParm sender Type(*Object)
		DclSrParm e      Type(System.Windows.Forms.FormClosingEventArgs)
        
        Close *All
	EndSr 

	BegSr Dispose Access(*Protected) Modifier(*Overrides)
		DclSrParm disposing Type(*Boolean)

		If (disposing *And (components *NE *Nothing))
			components.Dispose()
		EndIf
			
		*Base.Dispose(disposing)
	EndSr
	
	// Create Department/File Group Lock record on DSSRLCK
	BegFunc LockDepartment Type(*String)
		
		Clear UDSSRLCK
		
		LOKEY  = 12
		LODEPT = Department
		LOUSR  = AS400Profile
		LOMBR  = ToMbr
		
		If %SubSt(ToMbr,1,3) = 'DPT'
			LOTYP = 'Weekly'
		Else
			LOTYP = 'Daily'
		EndIf
		
		LOMOD  = 'S'
		LODAT  = DateTime.Now
		
		Write UDSSRLCK

		
		LeaveSr 'Locked'
	EndFunc
		
	BegSr btnCancel_Click Access(*Private) Event(*this.btnCancel.Click)
		DclSrParm sender *Object
		DclSrParm e System.EventArgs
		
		*this.DialogResult = DialogResult.Cancel
		*this.Close()
	EndSr		
EndClass
