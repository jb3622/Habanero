Using System
Using System.Text
Using System.Drawing

Using Disney.SRR

DclNameSpace Disney.SRR

BegClass SRRgrid6 Access(*Public)

	/Region Declare DataBase
	DclDB Name(SRR) DBName("TDST")
			
	DclDiskFile Name(DSCNTAP) Type(*Input) Org(*Indexed) +
		DB(SRR) File('*Libl/DSCNTAP3') ImpOpen(*No)
	
	DclDiskFile Name(IPCLASSB) Type(*Input) Org(*Indexed) +
		DB(SRR) File('*Libl/IPCLASSB') ImpOpen(*No)
		
	DclDiskFile Name(DSCLASS) Type(*Input) Org(*Indexed) +
		DB(SRR) File('*Libl/DSCLASS') ImpOpen(*No)
	/EndRegion
	
	DclFld  SRRkeyColor  Type(Disney.SRR.SRRkeyColor)
	DclFld  SRRuplift    Type(Disney.SRR.SRRuplift)
	DclFld  SRRsmooth    Type(Disney.SRR.SRRsmooth)
	DclFld  SRRcutOff    Type(Disney.SRR.SRRcutOff)
	DclFld  SRRalloc     Type(Disney.SRR.SRRalloc)
	
	DclArray ClassArray  Type(*Zoned) Len(4,0) Dim(50)
	DclFld   NoOfClasses Type(*Integer4)
	DclArray MarketArray Type(*Char)  Len(2)   Dim(9)
	DclFld   NoOfMarkets Type(*Integer4)
	DclFld   Department  Type(*Zoned) Len(3,0)
	DclFld   ErrorCount  Type(*Integer2) Inz(*Zero)
	
	DclEvent UpLift_Changed Access(*Public)
		DclSrParm Type(*String)
	
	DclEvent Smooth_Changed Access(*Public)
		DclSrParm Type(*String)
	
	DclEvent CutOff_Changed Access(*Public)
		DclSrParm Type(*String)
	
	DclEvent Alloc_Changed  Access(*Public)
		DclSrParm Type(*String)
	
	DclFld  Filtering     Type(*Boolean)
	DclFld  _SelectedRow  Type(*Integer4)
	DclFld  _GridCleared  Type(*Boolean)

	/Region Constructor & Destructor
	BegConstructor Access(*Public)
		DclSrParm SRR        Type(ASNA.VisualRPG.Runtime.Database)
		DclSrParm Department  Type(*Zoned) Len(3,0)
		DclSrParm SRRkeyColor Type(Disney.SRR.SRRkeyColor)
		DclSrParm SRRuplift   Type(Disney.SRR.SRRuplift)
		DclSrParm SRRsmooth   Type(Disney.SRR.SRRsmooth)
		DclSrParm SRRcutOff   Type(Disney.SRR.SRRcutOff)
		DclSrParm SRRalloc    Type(Disney.SRR.SRRalloc)
		
		*this.SRR           = SRR
		*this.Department    = Department
		*this.SRRkeyColor   = SRRkeyColor
		*this.SRRuplift    = SRRuplift
		*this.SRRsmooth    = SRRsmooth
		*this.SRRcutOff    = SRRcutoff
		*this.SRRalloc     = SRRalloc
				
		LoadClassArray()
		LoadMarketArray()
	EndConstructor
	/EndRegion
	
	/Region Property definitions
	BegProp HasValidationErrors Type(*Boolean) Access(*Public)
		BegGet
			If ErrorCount > *Zero
				LeaveSR *True
			Else
				LeaveSR *False
			EndIf
		EndGet
	EndProp
	
	BegProp isFiltered Type(*Boolean) Access(*Public)
		BegGet
			LeaveSR Filtering
		EndGet
	EndProp
	
	BegProp SelectedRow Type(*Integer4) Access(*Public)
		BegGet
			LeaveSR _SelectedRow
		EndGet
		BegSet
			_SelectedRow = *PropVal
		EndSet
	EndProp
	
	BegProp GridCleared Type(*Boolean) Access(*Public)
		BegGet
			LeaveSR _GridCleared
		EndGet
	EndProp
	/EndRegion

	BegSR Fill_Grid Access(*Public)
		DclSrParm Grid6      Type(System.Windows.Forms.DataGridView)
		DclSrParm SRRdetails Type(Disney.SRR.SRRdetails)
		
		DclFld _Uplift Type(*Zoned)  Len(3,2)
		DclFld _Smooth Type(*Zoned)  Len(5,2)
		DclFld _CutOff Type(*Zoned)  Len(5,2)
		DclFld _Alloc  Type(*Char)   Len(1)
	
		DclFld Row       Type(*Integer4)
		DclFld RowCount  Type(*Integer4)
		DclFld ColorChar Type(*String)
		
		DclFld Class       Type(*Zoned) Len(4,0)
		DclFld ClassIndex  Type(*Integer4)
		DclFld Market      Type(*Char)  Len(2)
		DclFld MarketIndex Type(*Integer4)
		
		Grid6.Rows.Clear()
		
		// Enumerate Classes
		Do FromVal(0) ToVal(NoOfClasses) Index(ClassIndex)
			Class = ClassArray(ClassIndex)
			
			// Enumerate Markets
			Do FromVal(0) ToVal(NoOfMarkets) Index(MarketIndex)
				Market = MarketArray(MarketIndex)

				// Find Class defaults
				_UpLift = SRRuplift.GetDftUplift(Class, Market)
				_Smooth = SRRsmooth.GetDftSmooth(Class, Market)
				_CutOff = SRRcutoff.GetDftCutOff(Class, Market)
				_Alloc  = SRRalloc.GetDftAlloc(Class, Market)


				Grid6.Rows.Add()
				RowCount = Grid6.Rows.Count
				Row = RowCount - 1

				ColorChar = SRRkeyColor.MarketColor(Market)

				Grid6.Rows[Row].Cells['G6Class'].Value = Class
				Grid6.Item['G6Class',Row].Style.BackColor = Color.FromArgb(Convert.ToInt32(ColorChar))

				Grid6.Rows[Row].Cells['G6Market'].Value = Market
				Grid6.Item['G6Market',Row].Style.BackColor = Color.FromArgb(Convert.ToInt32(ColorChar))

				Grid6 = Populate_Row(Grid6, Row, Class, Market, +
					_UpLift, _Smooth, _CutOff, _Alloc)

				
				// Stock requirement - by Store
				If SRRdetails <> *Nothing
					SRRdetails.GetClsReq(Class, Market)
					If SRRdetails.StockReq <> *Zero
						Grid6.Item['G6StkReq',Row].Value = SRRdetails.StockReq
					EndIf
	
					If SRRdetails.IdealQty <> *Zero
						Grid6.Item['G6IdealQty',Row].Value = SRRdetails.IdealQty
					EndIf
						
					If SRRdetails.ProposedQty <> *Zero
						Grid6.Item['G6PropQty',Row].Value = SRRdetails.ProposedQty
					EndIf
				EndIf
			EndDo
		EndDo
		
		DoEvents
		
		Filtering       = *False
		_SelectedRow    = -1
		_GridCleared    = *False
	EndSR
	
	BegSR Fill_Grid Access(*Public)
		DclSrParm Grid6      Type(System.Windows.Forms.DataGridView)
		DclSrParm SRRdetails Type(Disney.SRR.SRRdetails)
		DclSrParm Market     Type(*Char) Len(2)
		
		DclFld _Uplift Type(*Zoned)  Len(3,2)
		DclFld _Smooth Type(*Zoned)  Len(5,2)
		DclFld _CutOff Type(*Zoned)  Len(5,2)
		DclFld _Alloc  Type(*Char)   Len(1)
	
		DclFld Row       Type(*Integer4)
		DclFld RowCount  Type(*Integer4)
		DclFld ColorChar Type(*String)
		
		DclFld Class       Type(*Zoned) Len(4,0)
		DclFld ClassIndex  Type(*Integer4)
		
		Grid6.Rows.Clear()
		
		// Enumerate Classes
		Do FromVal(0) ToVal(NoOfClasses) Index(ClassIndex)
			Class = ClassArray(ClassIndex)
			

			// Find Class defaults
			_UpLift = SRRuplift.GetDftUplift(Class, Market)
			_Smooth = SRRsmooth.GetDftSmooth(Class, Market)
			_CutOff = SRRcutoff.GetDftCutOff(Class, Market)
			_Alloc  = SRRalloc.GetDftAlloc(Class, Market)


			Grid6.Rows.Add()
			RowCount = Grid6.Rows.Count
			Row = RowCount - 1

			ColorChar = SRRkeyColor.MarketColor(Market)

			Grid6.Rows[Row].Cells['G6Class'].Value = Class
			Grid6.Item['G6Class',Row].Style.BackColor = Color.FromArgb(Convert.ToInt32(ColorChar))

			Grid6.Rows[Row].Cells['G6Market'].Value = Market
			Grid6.Item['G6Market',Row].Style.BackColor = Color.FromArgb(Convert.ToInt32(ColorChar))

			Grid6 = Populate_Row(Grid6, Row, Class, Market, +
				_UpLift, _Smooth, _CutOff, _Alloc)

				
			// Stock requirement - by Store
			If SRRdetails <> *Nothing
				SRRdetails.GetClsReq(Class, Market)
				If SRRdetails.StockReq <> *Zero
					Grid6.Item['G6StkReq',Row].Value = SRRdetails.StockReq
				EndIf

				If SRRdetails.IdealQty <> *Zero
					Grid6.Item['G6IdealQty',Row].Value = SRRdetails.IdealQty
				EndIf
					
				If SRRdetails.ProposedQty <> *Zero
					Grid6.Item['G6PropQty',Row].Value = SRRdetails.ProposedQty
				EndIf
			EndIf
		EndDo
		
		DoEvents
		
		Filtering     = *True
		_SelectedRow  = -1
		_GridCleared  = *False
	EndSR
	
	BegSR Fill_Grid Access(*Public)
		DclSrParm Grid6      Type(System.Windows.Forms.DataGridView)
		DclSrParm SRRdetails Type(Disney.SRR.SRRdetails)
		DclSrParm Class      Type(*Zoned) Len(4,0)
		
		DclFld _Uplift Type(*Zoned)  Len(3,2)
		DclFld _Smooth Type(*Zoned)  Len(5,2)
		DclFld _CutOff Type(*Zoned)  Len(5,2)
		DclFld _Alloc  Type(*Char)   Len(1)
	
		DclFld Row       Type(*Integer4)
		DclFld RowCount  Type(*Integer4)
		DclFld ColorChar Type(*String)
		
		DclFld Market      Type(*Char)  Len(2)
		DclFld MarketIndex Type(*Integer4)
		
		Grid6.Rows.Clear()
					
		// Enumerate Markets
		Do FromVal(0) ToVal(NoOfMarkets) Index(MarketIndex)
			Market = MarketArray(MarketIndex)

			// Find Class defaults
			_UpLift = SRRuplift.GetDftUplift(Class, Market)
			_Smooth = SRRsmooth.GetDftSmooth(Class, Market)
			_CutOff = SRRcutoff.GetDftCutOff(Class, Market)
			_Alloc  = SRRalloc.GetDftAlloc(Class, Market)


			Grid6.Rows.Add()
			RowCount = Grid6.Rows.Count
			Row = RowCount - 1

			ColorChar = SRRkeyColor.MarketColor(Market)

			Grid6.Rows[Row].Cells['G6Class'].Value = Class
			Grid6.Item['G6Class',Row].Style.BackColor = Color.FromArgb(Convert.ToInt32(ColorChar))

			Grid6.Rows[Row].Cells['G6Market'].Value = Market
			Grid6.Item['G6Market',Row].Style.BackColor = Color.FromArgb(Convert.ToInt32(ColorChar))

			Grid6 = Populate_Row(Grid6, Row, Class, Market, +
				_UpLift, _Smooth, _CutOff, _Alloc)

				
			// Stock requirement - by Store
			If SRRdetails <> *Nothing
				SRRdetails.GetClsReq(Class, Market)
				If SRRdetails.StockReq <> *Zero
					Grid6.Item['G6StkReq',Row].Value = SRRdetails.StockReq
				EndIf

				If SRRdetails.IdealQty <> *Zero
					Grid6.Item['G6IdealQty',Row].Value = SRRdetails.IdealQty
				EndIf
					
				If SRRdetails.ProposedQty <> *Zero
					Grid6.Item['G6PropQty',Row].Value = SRRdetails.ProposedQty
				EndIf
			EndIf
		EndDo
		
		DoEvents
		
		Filtering       = *True
		_SelectedRow    = -1
		_GridCleared    = *False
	EndSR
	
	BegSR ClearGrid Access(*Public)
		DclSrParm Grid6  Type(System.Windows.Forms.DataGridView)
		
		Grid6.Rows.Clear()
		_GridCleared = *True
	EndSR
	
	BegFunc Populate_Row Type(system.Windows.Forms.DataGridView)
		DclSrParm Grid6   Type(system.Windows.Forms.DataGridView)
		DclSrParm Row     Type(*Integer4)
		DclSrParm Class   Type(*Zoned) Len(4,0)
		DclSrParm Market  Type(*Char)  Len(2)
		DclSrParm _UpLift Type(*Zoned) Len(3,2)
		DclSrParm _Smooth Type(*Zoned) Len(5,2)
		DclSrParm _CutOff Type(*Zoned) Len(5,2)
		DclSrParm _Alloc  Type(*Char)  Len(1)
		
		DclFld BoldFont    Type(System.Drawing.Font) New(Grid6.Font, FontStyle.Bold)
		DclFld RegularFont Type(System.Drawing.Font) New(Grid6.Font, FontStyle.Regular)
		
		DclFld Uplift Type(*Zoned)  Len(3,2)
		DclFld CutOff Type(*Zoned)  Len(5,2)
		DclFld Alloc  Type(*Char)   Len(1)

	
		// Uplift factor
		SRRuplift.GetUplift(Class, Market)
		If SRRuplift.IsFound
			UpLift = SRRuplift.Value
			Grid6.Item['G6UpLift',Row].Style.Font = RegularFont
			
			If SRRuplift.IsChanged
				Grid6.Item['G6UpLift',Row].Style.ForeColor = Color.Red
			Else
				Grid6.Item['G6UpLift',Row].Style.ForeColor = Color.Black
			EndIf
		Else
			UpLift = _Uplift
			Grid6.Item['G6UpLift',Row].Style.ForeColor = Color.Blue
			Grid6.Item['G6UpLift',Row].Style.Font      = BoldFont
		EndIf
		If Uplift <> *Zero
			Grid6.Rows[Row].Cells['G6UpLift'].Value = Uplift
		EndIf
		If SRRuplift.ExceptionsBelow
			Grid6.Item['G6UpLift',Row].Style.BackColor = Color.Yellow
		EndIf


		// Smoothing factor
		SRRsmooth.GetSmooth(Class, Market)
		If SRRsmooth.IsFound
			Grid6.Item['G6Smooth',Row].Value      = SRRsmooth.Value
			Grid6.Item['G6Smooth',Row].Style.Font = RegularFont
			
			If SRRsmooth.IsChanged
				Grid6.Item['G6Smooth',Row].Style.ForeColor = Color.Red
			Else
				Grid6.Item['G6Smooth',Row].Style.ForeColor = Color.Black
			EndIf
		Else
			Grid6.Item['G6Smooth',Row].Value           = _Smooth
			Grid6.Item['G6Smooth',Row].Style.ForeColor = Color.Blue
			Grid6.Item['G6Smooth',Row].Style.Font      = BoldFont
		EndIf
		If SRRsmooth.ExceptionsBelow
			Grid6.Item['G6Smooth', Row].Style.BackColor = Color.Yellow
		EndIf
		
				
		// Cover Cut-Off factor
		SRRcutoff.GetCutOff(Class, Market)
		If SRRcutoff.IsFound
			CutOff = SRRcutoff.Value
			Grid6.Item['G6CutOff',Row].Style.Font = RegularFont
			
			If SRRcutoff.IsChanged
				Grid6.Item['G6CutOff',Row].Style.ForeColor = Color.Red
			Else
				Grid6.Item['G6CutOff',Row].Style.ForeColor = Color.Black
			EndIf
		Else
			CutOff = _CutOff
			Grid6.Item['G6CutOff',Row].Style.ForeColor = Color.Blue
			Grid6.Item['G6CutOff',Row].Style.Font      = BoldFont
		EndIf
		If CutOff <> *Zero
			Grid6.Item['G6CutOff',Row].Value = CutOff
		EndIf
		If SRRcutoff.ExceptionsBelow
			Grid6.Item['G6CutOff',Row].Style.BackColor = Color.Yellow
		EndIf
				
				
		// Allocation flag
		SRRalloc.GetAlloc(Class, Market)
		If SRRalloc.IsFound
			Alloc = SRRalloc.Value
			Grid6.Item['G6Alloc',Row].Style.Font = RegularFont
			
			If SRRalloc.IsChanged
				Grid6.Item['G6Alloc',Row].Style.ForeColor = Color.Red
			Else
				Grid6.Item['G6Alloc',Row].Style.ForeColor = Color.Black
			EndIf
		Else
			Alloc = _Alloc
			Grid6.Item['G6Alloc',Row].Style.ForeColor = Color.Blue
			Grid6.Item['G6Alloc',Row].Style.Font      = BoldFont
		EndIf
		If Alloc <> *Blank
			Grid6.Rows[Row].Cells['G6Alloc'].Value = Alloc
		EndIf
		If SRRalloc.ExceptionsBelow
			Grid6.Item['G6Alloc',Row].Style.BackColor = Color.Yellow
		EndIf
		
		LeaveSR Grid6			
	EndFunc
	
	BegSR CellValueChanged Access(*Public)
		DclSrParm sender Type(*Object)
		DclSrParm e      Type(System.Windows.Forms.DataGridViewCellEventArgs)
		DclSrParm Grid6  Type(System.Windows.Forms.DataGridView)

		DclFld BoldFont   Type(System.Drawing.Font) New(Grid6.Font, FontStyle.Bold)
		DclFld RegularFont  Type(System.Drawing.Font) New(Grid6.Font, FontStyle.Regular)

		DclFld ColumnName Type( *String )
		DclFld CellValue  Type( *String )
		
		DclFld Column Type( *Integer4 )
		DclFld Row    Type( *Integer4 )

		DclFld Class  Type(*Zoned) Len(4,0)
		DclFld Market Type(*Char)  Len(2)
		DclFld Uplift Type(*Zoned) Len(5,2)
		DclFld Smooth Type(*Zoned) Len(5,2)
		DclFld CutOff Type(*Zoned) Len(5,2)
		DclFld Alloc  Type(*Char)  Len(1)

		Column = e.ColumnIndex
		Row    = e.RowIndex
		
		If Column >= *Zero and Row >= *Zero
			ColumnName = Grid6.Columns[Column].Name.ToUpper()
			Try
				CellValue = Grid6.Item[Column, Row].Value.ToString().Trim()
			Catch Err Exception
				CellValue = String.Empty
			EndTry
			
			// Get Market value from grid
			Class = Convert.ToInt32(Grid6.Item['G6Class', Row].Value.ToString().Trim())
			Market = Grid6.Item['G6Market', Row].Value.ToString().Trim()

			Select
			When ColumnName = 'G6UPLIFT'
				If Grid6.Item[Column,Row].ErrorText <> String.Empty
					ErrorCount -= 1
					Grid6.Item[Column,Row].ErrorText = String.Empty
				EndIf
				
				If CellValue = String.Empty
					Grid6.Item[Column, Row].Value           = SRRuplift.GetDftUplift(Class, Market)
					Grid6.Item[Column, Row].Style.ForeColor = Color.Blue
					Grid6.Item[Column, Row].Style.Font      = BoldFont
					
					SRRuplift.DeleteUplift(Class, Market)
				Else
					Try
						Uplift = Convert.ToDecimal( CellValue )
					Catch Err Exception
						grid6.Item[Column,Row].ErrorText = 'Invalid Number'
						ErrorCount += 1
						LeaveSR
					EndTry
					
					If Uplift < 0.01 Or Uplift > 9.99
						Grid6.Item[Column,Row].ErrorText = 'Value must be between Zero and Ten'
						ErrorCount += 1
						LeaveSR
					EndIf
					
					Grid6.Item[Column,Row].Style.ForeColor = Color.Red
					Grid6.Item[Column,Row].Style.Font      = RegularFont
					
					SRRuplift.UpdateUplift(Class, Market, UpLift)
					Uplift_Changed('Grid6')
				EndIf
				
					
			When ColumnName = 'G6SMOOTH'
				If Grid6.Item[Column,Row].ErrorText <> String.Empty
					ErrorCount -= 1
					Grid6.Item[Column,Row].ErrorText = String.Empty
				EndIf
				
				If CellValue = String.Empty
					Grid6.Item[Column, Row].Value           = SRRsmooth.GetDftSmooth(Class, Market)
					Grid6.Item[Column, Row].Style.ForeColor = Color.Blue
					Grid6.Item[Column, Row].Style.Font      = BoldFont
					
					SRRsmooth.DeleteSmooth(Class, Market)
				Else
					Try
						Smooth = Convert.ToDecimal( CellValue )
					Catch Err Exception
						Grid6.Item[Column,Row].ErrorText = 'Invalid Number'
						ErrorCount += 1
						LeaveSR
					EndTry
					
					If Smooth > 1 Or Smooth < *Zero
						Grid6.Item[Column,Row].ErrorText = 'Value must be between 0 and 1'
						ErrorCount += 1
						LeaveSR
					EndIf
				
					Grid6.Item[Column,Row].Style.ForeColor = Color.Red
					Grid6.Item[Column,Row].Style.Font      = RegularFont
				
					SRRsmooth.UpdateSmooth(Class, Market, Smooth)
					Smooth_Changed('Grid6')
				EndIf
				
				
			When ColumnName = 'G6CUTOFF'
				If Grid6.Item[Column,Row].ErrorText <> String.Empty
					ErrorCount -= 1
					Grid6.Item[Column,Row].ErrorText = String.Empty
				EndIf
				
				If CellValue = String.Empty
					Grid6.Item[Column, Row].Value           = SRRcutoff.GetDftCutOff(Class, Market)
					Grid6.Item[Column, Row].Style.ForeColor = Color.Blue
					Grid6.Item[Column, Row].Style.Font      = BoldFont
					
					SRRcutoff.DeleteCutOff(Class, Market)
				Else
					Try
						CutOff = Convert.ToDecimal( CellValue )
					Catch Err Exception
						grid6.Item[Column,Row].ErrorText = 'Invalid Number'
						ErrorCount += 1
						LeaveSR
					EndTry
					
					If CutOff <= *Zero
						grid6.Item[Column,Row].ErrorText = 'Value must be greater than zero'
						ErrorCount += 1
						LeaveSR
					EndIf
					
					Grid6.Item[Column, Row].Style.ForeColor = Color.Red
					Grid6.Item[Column, Row].Style.Font      = RegularFont
					
					SRRcutoff.UpdateCutOff(Class, Market, CutOff)
					CutOff_Changed('Grid6')
				EndIf
				
				
			When ColumnName = 'G6ALLOC'
				If Grid6.Item[Column,Row].ErrorText <> String.Empty
					ErrorCount -= 1
					grid6.Item[Column,Row].ErrorText = String.Empty
				EndIf
				
				If CellValue = String.Empty
					Grid6.Item[Column, Row].Value           = SRRalloc.GetDftAlloc(Class, Market)
					Grid6.Item[Column, Row].Style.ForeColor = Color.Blue
					Grid6.Item[Column, Row].Style.Font      = BoldFont
					
					SRRalloc.DeleteAlloc(Class, Market)
				Else
					Alloc = CellValue.ToUpper().Trim()
					If Alloc = 'Y' Or Alloc = 'N'
						Grid6.Item[Column, Row].Value = Alloc
						Grid6.Item[Column,Row].Style.ForeColor = Color.Red
						Grid6.Item[Column,Row].Style.Font      = RegularFont
					
						SRRalloc.UpdateAlloc(Class, Market, Alloc)
						Alloc_Changed('Grid6')
					Else
						grid6.Item[Column,Row].ErrorText = 'Invalid Flag (Must be Y or N)'
						ErrorCount += 1
						LeaveSR
					EndIf
				EndIf
			EndSL
		EndIf
	EndSR
	
	BegSr RmvExceptions Access(*Public)
		DclSrParm Grid6 Type(System.Windows.Forms.DataGridView)
	
		DclFld Class  Type(*Zoned)  Len(4,0)
		DclFld Market Type(*Char)   Len(2)
		DclFld Row    Type(*Integer2)
		
		Do FromVal(0) ToVal(Grid6.Rows.Count-1) Index(Row)
			Class = Convert.ToInt32(Grid6.Item['G6Class', Row].Value)
			Market = Grid6.Item['G6Market', Row].Value.ToString().Trim()

			// Find Dept/Market/DeptGrade/Store level records and delete
			SRRuplift.DeleteUpLift(Class, Market)
			SRRsmooth.DeleteSmooth(Class, Market)
			SRRcutoff.DeleteCutOff(Class, Market)
			SRRalloc.DeleteAlloc(Class, Market)
			
			ErrorCount = *Zero
		EndDo
	EndSr

	// Load Class Array
	BegSr LoadClassArray
			
		If Not IPCLASSB.IsOpen
			Open IPCLASSB
		EndIf
		
		If Not DSCLASS.IsOpen
			Open DSCLASS
		EndIf
		
		NoOfClasses = -1
		SetRange IPCLASSB FirstKey(Department)
		DoUntil IPCLASSB.IsEof
			Read IPCLASSB
			If Not IPCLASSB.IsEof
				Chain DSCLASS Key(CCLS)
				If DSCLASS.IsFound And CCODE = 'Y'
					NoOfClasses += 1
					ClassArray(NoOfClasses) = CCLS
				EndIf
			EndIf
		EndDo
		
		Close IPCLASSB
		Close DSCLASS
	EndSr
	
	// Load array of Markets
	BegSr LoadMarketArray
			
		If Not DSCNTAP.IsOpen
			Open DSCNTAP
		EndIf
		
		NoOfMarkets = -1
		SetLL DSCNTAP Key(*Start)
		DoUntil DSCNTAP.IsEof
			Read DSCNTAP
			If Not DSCNTAP.IsEof
				If CSDGRP <> *Blank And CSBGRP <> *Blank And CSCODE <> 'CA'
					NoOfMarkets += 1
					MarketArray(NoOfMarkets) = CSCODE
				EndIf
			EndIf
		EndDo
		
		Close DSCNTAP
	EndSr
EndClass
