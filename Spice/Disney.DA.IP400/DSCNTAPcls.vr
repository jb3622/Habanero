// ===============================================
// Joseph Urbina
// 27th May 2009
// DSCNTAPcls	
// Data Access Layer - Country Selection File
//------------------------------------------------
// Modification
//================================================

Using System
Using System.Text
Using System.Data
Using System.Collections

BegClass DSCNTAPcls Access(*Public)
	
	/Region Declare Files and DB
	DclDB Name(PgmDB) DbName("TDST")
	DclDiskFile Name(DSCNTAP) Type(*Update) Org(*Indexed) ImpOpen(*No) DB(PgmDB) AddRec(*Yes) RnmFmt(DSCS01:R_DSCS01)
	DclDiskFile Name(DSCNTAP4) Type(*Input) Org(*Indexed) ImpOpen(*No) DB(PgmDB) RnmFmt(DSCS01:R_DSCS04) 
	/EndRegion
	
	/Region Constructors
	BegConstructor Access(*Public)
		DclSrParm PgmDB Type(ASNA.VisualRPG.Runtime.Database)
		
		*this.PgmDB = PgmDB
				
	EndConstructor
	/EndRegion
	
	/Region DSCNTAP Properties
	BegProp Country_Code Type(String) Access(*Public)
		BegGet
			LeaveSR CSCODE
		EndGet
	EndProp
	
	BegProp Country_Desc Type(String) Access(*Public)
		BegGet
			LeaveSR CSDESC
		EndGet
		
		BegSet
			CSDESC = *PropVal
		EndSet
	EndProp
	
	BegProp MrchLibName Type(String) Access(*Public)
		BegGet
			LeaveSR CSLIBM
		EndGet
	EndProp	
	/EndRegion
	
	/Region Methods
	BegFunc GetCountryCode Type(*Boolean) Access(*Public)
		DclSrParm Country_Code Type(String)
		
		If Not DSCNTAP4.IsOpen
			Open DSCNTAP4
		EndIf
		
		Chain From(DSCNTAP4) Key(Country_Code) Access(*NoLock)
		If %Found
			LeaveSR *Off
		Else
			LeaveSR *On
		EndIf
	EndFunc
	
	BegFunc GetCountryCodeForUpdate Type(*Boolean) Access(*Public)
		DclSrParm Country_Code Type(String)
		
		If Not DSCNTAP.IsOpen
			Open DSCNTAP
		EndIf
		
		Chain From(DSCNTAP) Key(Country_Code) Access(*DFT) Err(*Extended)
		If not %Found
			LeaveSR *On
		EndIf
		
		If %Error = *On
			Throw *New System.Exception('Failed to get DSCNTAP record for update')
			LeaveSR *On
		EndIf
		
		LeaveSR *Off
	EndFunc
	
	BegSR WriteCountry_Code Access(*Public)
		DclSrParm Country_Code Type(String)
	
		If Not DSCNTAP.IsOpen
			Open DSCNTAP
		EndIf
		
		CSCODE = Country_Code
		
		Write R_DSCS01 Err(*Extended)
		If %Error = *On
			Throw *New System.Exception('Failed to write DSCNTAP record')
		EndIf
		
		LeaveSR
	EndSR
	
	BegFunc UpdateCountry_Code Type(*Boolean) Access(*Public) 
		DclSrParm Country_Code Type(String)
		
		Update R_DSCS01 Err(*Extended)
		If %Error = *On
			Throw *New System.Exception('Failed to update DSCNTAP record')
			LeaveSR *True
		EndIf
		
		LeaveSR *Off
	EndFunc
	
	BegFunc DeleteCountry_Code Type(*Boolean) Access(*Public)
		DclSrParm Country_Code Type(String)
		
		If Not DSCNTAP.IsOpen
			Open DSCNTAP
		EndIf
		
		Delete From(R_DSCS01) Key(Country_Code) Err(*Extended)
		If %Error = *On
			LeaveSR *True
		EndIf
		
		LeaveSR *False
	EndFunc
	
	//Creates a DataSet/DataTable for Market
	BegFunc GetCountryCodeDataSet Type(DataTable) Access(*Public)
			
		DclFld dt Type( DataTable ) New()
		DclFld dc Type( DataColumn ) 
		DclFld dr Type( DataRow )
		
										
		If Not DSCNTAP4.IsOpen
			Open DSCNTAP4
		EndIf
		
		//Create Country Code Column
		dc = *New DataColumn( 'clmCSCODE' )
		dc.DataType = System.Type.GetType("System.String")
		dc.ColumnName = "clmCSCODE"
		dc.Caption = "Country Code"
		dt.Columns.Add(dc)
		
		//Create Country Description Column
		dc = *New DataColumn( 'clmCSDESC' )
		dc.DataType = System.Type.GetType("System.String")
		dc.ColumnName = "clmCSDESC"
		dc.Caption = "Country Description"
		dt.Columns.Add(dc)
		
		//Read file and create rows in DataTable				
		Setll DSCNTAP4 Key(*Start)
		DoUntil DSCNTAP4.IsEof
			Read DSCNTAP4
			If not DSCNTAP4.IsEof
				dr = dt.NewRow()
				dr.Item[0] = CSCODE
				dr.Item[1] = CSDESC
								
				dt.Rows.Add( dr )
			Endif
		Enddo
		
		LeaveSR dt
	EndFunc
	
	//Creates a DataSet/DataTable for Market Searched by Market Description
	BegFunc GetCountryCodeDataSet Type(DataTable) Access(*Public)
		DclSrParm MarketDesc Type(String)
			
		DclFld dt Type( DataTable ) New()
		DclFld dc Type( DataColumn ) 
		DclFld dr Type( DataRow )
		DclFld Name(Pos) Type(*ZONED) Len(5,0)
		
										
		If Not DSCNTAP4.IsOpen
			Open DSCNTAP4
		EndIf
		
		//Create Country Code Column
		dc = *New DataColumn( 'clmCSCODE' )
		dc.DataType = System.Type.GetType("System.String")
		dc.ColumnName = "clmCSCODE"
		dc.Caption = "Country Code"
		dt.Columns.Add(dc)
		
		//Create Country Description Column
		dc = *New DataColumn( 'clmCSDESC' )
		dc.DataType = System.Type.GetType("System.String")
		dc.ColumnName = "clmCSDESC"
		dc.Caption = "Country Description"
		dt.Columns.Add(dc)
		
		//Read file and create rows in DataTable				
		Setll DSCNTAP4 Key(*Start)
		DoUntil DSCNTAP4.IsEof
			Read DSCNTAP4
			Pos = %Scan (MarketDesc.Trim(), CSDESC.Trim(), 1)
			If Pos <> 0
				If not DSCNTAP4.IsEof
					dr = dt.NewRow()
					dr.Item[0] = CSCODE
					dr.Item[1] = CSDESC
								
					dt.Rows.Add( dr )
				Endif
			Endif
		Enddo
		
		LeaveSR dt
	EndFunc
	
	//Creates a DataSet/DataTable for Market Excluding Default Market Currency(Used for the Pricing Matrix).
	BegFunc GetCurrencyCodeDataSet Type(DataTable) Access(*Public)
		DclSrParm DefaultMarketCurrencyCode Type(String)
			
		DclFld dt Type( DataTable ) New()
		DclFld dc Type( DataColumn ) 
		DclFld dr Type( DataRow )
										
		If Not DSCNTAP4.IsOpen
			Open DSCNTAP4
		EndIf
		
		//Create Country Code Column
		dc = *New DataColumn( 'clmCurrency' )
		dc.DataType = System.Type.GetType("System.String")
		dc.ColumnName = "clmCSCUR#"
		dt.Columns.Add(dc)
						
		//Read file and create rows in DataTable
		Setll DSCNTAP4 Key(*Start)
		DoUntil DSCNTAP4.IsEof
			Read DSCNTAP4
			If not DSCNTAP4.IsEof And CSCUR# <> DefaultMarketCurrencyCode
				dr = dt.NewRow()
				dr.Item[0] = CSCUR#
												
				dt.Rows.Add( dr )
			Endif
		Enddo
		
		LeaveSR dt
	EndFunc
	/EndRegion
EndClass
