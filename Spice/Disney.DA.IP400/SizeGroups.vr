Using System
Using System.IO
Using System.Data
Using System.Text
Using System.Xml

BegClass SizeGroups Access(*Public)

	DclDB PgmDB DBName("TDST") Access(*Private)
	DclDiskFile Name(DSSPSGHL01) Type(*Input) Org(*Indexed) DB(PgmDB) ImpOpen(*No)
	
	DclFld SizeGroupsTbl Type(DataTable) Access(*Private)
	DclFld pXMLpath      Type(String)    Access(*Private)
	
	BegConstructor Access(*Public)
		DclSrParm PgmDB Type(ASNA.VisualRPG.Runtime.Database)
		DclSrParm Environment Type(Disney.Menu.Environments)
		
		*this.PgmDB = PgmDB
		
		pXMLpath = Path.Combine(Environment.PathToApplicationXML,"SizeGroups.xml")
		LoadDataTableFromXml()
	EndConstructor
	
	BegSr LoadDataTableFromXml Access(*Private)
			
		If SizeGroupsTbl = *Nothing
			CreateTbl()
			If Not File.Exists(pXMLpath)
				PopulateDataTable()
			Else
				SizeGroupsTbl.ReadXml(pXMLpath)
			EndIf
		EndIf
	EndSr
	
	BegSr CreateTbl Access(*Private)
	
		SizeGroupsTbl = *New DataTable("SizeGroups")

		SizeGroupsTbl.Columns.Add(*New DataColumn("Select", *TypeOf(Boolean)))

		SizeGroupsTbl.Columns.Add(*New DataColumn("Class", *TypeOf(Int16)))
		SizeGroupsTbl.Columns["Class"].ReadOnly = *True

		SizeGroupsTbl.Columns.Add(*New DataColumn("Group", *TypeOf(Int16)))
		SizeGroupsTbl.Columns["Group"].ReadOnly = *True
		
		SizeGroupsTbl.Columns.Add(*New DataColumn("Description", *TypeOf(String)))
		SizeGroupsTbl.Columns["Description"].ReadOnly = *True
		
		SizeGroupsTbl.Columns.Add(*New DataColumn("GroupDescription", *TypeOf(String)))
		SizeGroupsTbl.Columns["GroupDescription"].Expression = "Group + ' - ' + Description"
		SizeGroupsTbl.Columns["GroupDescription"].ReadOnly = *True
	EndSr
	
	BegSr PopulateDataTable Access(*Private)
				
		DclFld dr Type(DataRow)
			
		If Not DSSPSGHL01.IsOpen
			Open DSSPSGHL01
		EndIf
		
		DoUntil DSSPSGHL01.IsEof
			Read DSSPSGHL01
			If Not DSSPSGHL01.IsEof
				dr = SizeGroupsTbl.NewRow()
				
				dr["Class"]       = SGHCLS
				dr["Group"]       = SGHSGC
				dr["Description"] = SGHSGD.Trim()
				
				SizeGroupsTbl.Rows.Add(dr)
			EndIf
		EndDO
		
		SizeGroupsTbl.WriteXml(pXMLpath)
		
		Close DSSPSGHL01
	EndSr
	
	BegFunc GetSizeGroupTbl Type(DataTable) Access(*Public)
		
		LeaveSR SizeGroupsTbl
	EndFunc
EndClass