// ===============================================
// Tarnjit Bhambra
// 08 April 2009
// DSSPAPRcls	
// Data Access Layer - Approvers
//------------------------------------------------
// Modification
//================================================

Using System
Using System.Text

BegClass DSSPAPRcls Access(*Public)

	DclFld memRecords Type(*Integer4)
	DclFld NxtApproverID Type(*Zoned) Len(5,0)
	
	DclDB Name(DB) DbName("TDST")
	
	DclDiskFile Name(DSSPAPRL1) Type(*Update) Org(*Indexed) ImpOpen(*No) DB(DB) AddRec(*Yes) RnmFmt(Dssparr:R_Dssparr)
	
	DclMemoryFile Name(mDSSPAPR) DBdesc(DB) Filedesc('DSSPAPR') +
		RnmFmt(RDSSPAPR, mDSSPARR) ImpOpen(*No)
	
	BegConstructor Access(*Public)
		DclSrParm DB Type(ASNA.VisualRPG.Runtime.Database)
		
		*this.DB = DB
	EndConstructor
	
	BegConstructor Access(*Public)
		DclSrParm DB    Type(ASNA.VisualRPG.Runtime.Database)
		DclSrParm ApproverID Type(*Zoned) Len(5,0)
		
		*this.DB = DB
		
		GetApproverID(ApproverID)
	EndConstructor
	
		
	//Properties
	BegProp Approver_ID Type(*Zoned) Len(5,0) Access(*Public)
		BegGet
			LeaveSR APRID
		EndGet
	EndProp
	
	BegProp Approver_Name Type(*Char) Len(35) Access(*Public)
		BegGet
			LeaveSR APRNAM
		EndGet
		
		BegSet
			APRNAM = *PropVal
		EndSet
	EndProp
	
	BegProp Create_Date Type(*Zoned ) Len(8,0) Access(*Public)
		BegGet
			LeaveSR APRCRD
		EndGet
		
		BegSet
			APRCRD = *PropVal
		EndSet
	EndProp
	
	BegProp Create_Time Type(*Zoned ) Len(6,0) Access(*Public)
		BegGet
			LeaveSR APRCRT
		EndGet
		
		BegSet
			APRCRT = *PropVal
		EndSet
	EndProp
	
	BegProp Create_User Type(*Char ) len(10) Access(*Public)
		BegGet
			LeaveSR APRCRU 
		EndGet
		
		BegSet
			APRCRU = *PropVal
		EndSet
	EndProp
	
		BegProp Changed_Date Type(*Zoned ) Len(8,0) Access(*Public)
		BegGet
			LeaveSR APRCHD
		EndGet
		
		BegSet
			APRCHD = *PropVal
		EndSet
	EndProp
	
	BegProp Changed_Time Type(*Zoned ) Len(6,0) Access(*Public)
		BegGet
			LeaveSR APRCHT 
		EndGet
		
		BegSet
			APRCHT  = *PropVal
		EndSet
	EndProp
	
	BegProp Changed_User Type(*Char ) len(10) Access(*Public)
		BegGet
			LeaveSR APRCHU 
		EndGet
		
		BegSet
			APRCHU  = *PropVal
		EndSet
	EndProp
		
	// Methods
	BegFunc GetNextApproverID  Type(*Zoned) Len(5,0)  Access(*Public)
		
		If Not DSSPAPRL1.IsOpen
			Open DSSPAPRL1
		EndIf
		
		SetGt File(DSSPAPRL1) Key(*Hival)
		Readp  DSSPAPRL1
		If Not %Eof(DSSPAPRL1)
			NxtApproverID = APRID + 1
		  Else
		    NxtApproverID = 1	
		Endif
		
		LeaveSR NxtApproverID
	EndFunc
	
	BegFunc GetApproverID Type(*Boolean) Access(*Public)
		DclSrParm ApproverID Type(*Zoned) len(5,0)
		
		If Not DSSPAPRL1.IsOpen
			Open DSSPAPRL1
		EndIf
		
		Chain From(DSSPAPRL1) Key(ApproverID) Access(*NoLock)
		If %Found
			LeaveSR *Off
		Else
			LeaveSR *On
		EndIf
	EndFunc
	
	BegFunc GetApproverIDForUpdate Type(*Boolean) Access(*Public)
		DclSrParm ApproverID Type(*Zoned) len(5,0)
		
		If Not DSSPAPRL1.IsOpen
			Open DSSPAPRL1
		EndIf
		
		Chain From(DSSPAPRL1) Key(ApproverID) Access(*DFT) Err(*Extended)
		If Not %Found
			LeaveSR *On
		EndIf
		
		If %Error = *On
			Throw *New System.Exception('Failed to get record for update (DSSPAPRL01)')
			LeaveSR *On
		EndIf
		
		LeaveSR *Off
	EndFunc
		
	BegSR WriteApproverID Access(*Public)
		DclSrParm ApproverID Type(*Zoned) Len(5,0)
	
		If Not DSSPAPRL1.IsOpen
			Open DSSPAPRL1
		EndIf
		
		APRID = ApproverID
		APRCRU = DB.User.ToString().Trim()
		APRCRD = %Date
		APRCRT = %Time
		APRCHU = *Blanks
		APRCHD = *Zeros
		APRCHT = *Zeros
		
		Write RDsspapr Err(*Extended)
		If %Error = *On
			Throw *New System.Exception('Failed to write record (DSSPAPRL1)')
		EndIf
		
		Unlock DSSPAPRL1
		
		LeaveSR
	EndSR
	
	BegFunc UpdateApproverID Type(*Boolean) Access(*Public) 
		DclSrParm ApproverID Type(*Zoned) len(5,0)
		
		Update RDsspapr Err(*Extended)
		If %Error = *On
			Throw *New System.Exception('Failed to update record (DSSPAPRL1)')
			LeaveSR *True
		EndIf
		
		LeaveSR *Off
	EndFunc
	
	BegFunc DeleteApproverID Type(*Boolean) Access(*Public)
		DclSrParm ApproverID Type(*Zoned) len(5,0)
		
		If Not DSSPAPRL1.IsOpen
			Open DSSPAPRL1
		EndIf
		
		Delete From(RDsspapr) Key(ApproverID) Err(*Extended)
		If %Error = *On
			LeaveSR *True
		EndIf
		
		LeaveSR *False
	EndFunc
	
	/Region Memory files
	BegFunc setMemDSSPAPR  Type(*Boolean) Access(*Public)
	
		If Not DSSPAPRL1.IsOpen
			Open DSSPAPRL1
		EndIf
	
	 	If Not mDSSPAPR.IsOpen
	 		Open mDSSPAPR
		Endif	
		
		mDSSPAPR.DataSet.Clear()
		memRecords = *Zero
	
		SetLL DSSPAPRL1 key(*Start)
		DoUntil DSSPAPRL1.IsEof
			Read DSSPAPRL1
			If (Not DSSPAPRL1.IsEof)
			   Write mDSSPAPR
			   memRecords += 1
		    Endif
		Enddo
	    
	    Close DSSPAPRL1 
	
		LeaveSR *True
	EndFunc	
	
	BegFunc ReadDSSPAPR Type(*Boolean) Access(*Public)
		DclSrParm wIdx Type(*Integer2)
		
		If wIdx > memRecords 
		   LeaveSR *True
		Endif   
		
		Chain mDSSPAPR key(wIdx)
		If mDSSPAPR.IsEof
		   LeaveSR *True
		Endif
		
		LeaveSR *false   
	
	EndFunc
	/EndRegion
EndClass