Using System
Using System.Text
Using System.Data
Using System.Collections

BegClass ItemPos Access(*Public)

	/Region Declares
	DclDB Name(PgmDB) DbName("TDST")
	DclDiskFile Name(DSSPPOHL1) Type(*Input) Org(*Indexed) ImpOpen(*No) DB(PgmDB) 
	DclDiskFile Name(DSSPPOIL2) Type(*Input) Org(*Indexed) ImpOpen(*No) DB(PgmDB) 
	DclDiskFile Name(DSSPPOIL3) Type(*Input) Org(*Indexed) ImpOpen(*No) DB(PgmDb) RnmFmt(RDSSPPOI:POI3) 
		
	DclFld DateFormat Type(String)     Access(*Private)
	DclFld ItemPostbl Type(DataTable)  Access(*Private)
	DclFld ItemPosClm Type(DataColumn) Access(*Private)
	DclFld ItemPosRow Type(DataRow)    Access(*Private)
	
	DclFld Environment Type(Disney.Menu.Environments) Access(*Private)
	/EndRegion
	
	/Region Contructor
	BegConstructor Access(*Public)
		DclSrParm PgmDB Type(ASNA.VisualRPG.Runtime.Database)
		DclSrParm Environment Type(Disney.Menu.Environments)
		
		*this.PgmDB       = PgmDB
		*this.Environment = Environment
		*this.DateFormat  = Environment.DateFormat
	EndConstructor
	/EndRegion
	
	/Region Methods
	BegFunc GetItemPosTable Type(DataTable) Access(*Public)
		DclSrparm Class        Type(Int16)
		DclSrparm Vendor       Type(Int32)
		DclSrParm Style        Type(Int16)
		DclSrparm Colour       Type(Int16)
		DclSrParm Size		   Type(int16)
				
		DclFld QtyCalc     Type(Int32)
		DclFld CostCalc    Type(Decimal)
		DclFld RecordCount Type(Int16)
		DclFld WrkDate     Type(*Date) 
		DclFld IPMRVENcls  Type(IPMRVENcls)
		DclFld CheckVendor Type(Boolean)
		DclFld SaveSPO     Type(String)
				
		CreateItemPosTbl()
		
		If Not DSSPPOHL1.IsOpen
			Open DSSPPOHL1
		EndIf
		
		If Not DSSPPOIL2.IsOpen
			Open DSSPPOIL2
		EndIf
		
		If Not DSSPPOIL3.IsOpen
			Open DSSPPOIL3
		EndIf
		
		If IPMRVENcls = *Nothing
			IPMRVENcls = *New IPMRVENcls(PgmDB, Environment)
	    Endif
				
		//Read file and create rows in DataTable
		//Read all records for the given Item				
		Setll DSSPPOIL3 Key(Class, Vendor, Style, Colour, Size)
		DoUntil DSSPPOIL3.IsEof
			ReadE DSSPPOIL3 Key(Class, Vendor, Style, Colour, Size) Access(*NoLock)
			If not DSSPPOIL3.IsEof
			 
				RecordCount = 0
				QtyCalc     = 0
				CostCalc    = 0
				
				//Position on order and last version number and calculate qty and cost.
				//This is in case there is a duplicated item within the same order/version number
				Setll DSSPPOIL2 Key(Class, Vendor, Style, Colour, Size, POISPO) 
				DoUntil DSSPPOIL2.IsEof
				ReadE DSSPPOIL2 Key(Class, Vendor, Style, Colour, Size, POISPO, POIVER) Access(*NoLock)
					If not DSSPPOIL2.IsEof 
						RecordCount += 1
						QtyCalc     += POIQTY
						CostCalc    += POILNC
					Endif
				Enddo
				
				If RecordCount > 1
					CostCalc = CostCalc/RecordCount
				Endif
				
				//Get header info. Ignore duplicate order numbers as already catered for above.
				Chain From(DSSPPOHL1) Key(POISPO, POIVER) Access(*NoLock)
				If %Found 
					If SaveSPO <> POHSPO
						ItemPosRow = ItemPosTbl.NewRow()
						ItemPosRow.Item["SpicePO"]    = POHSPO
						SaveSPO = POHSPO
						ItemPosRow.Item["Revision"]   = POHRNO
						ItemPosRow.Item["IPPO"]       = POHIPO
						ItemPosRow.Item["Vendor"]     = POHVEN
						
						CheckVendor = IPMRVENcls.GetVendor(POHVEN)
						If CheckVendor = *True	
							ItemPosRow.Item["VendorName"] = IPMRVENcls.VendorName
						Else
							ItemPosRow.Item["VendorName"] = " "
						Endif
											
						If POHSDT <> *Zero
							Wrkdate = POHSDT
							If DateFormat = 'DMY'
								ItemPosRow.Item["ShipDate"]  = WrkDate.ToString('dd - MMM - yyyy').ToUpper()
							Else
								ItemPosRow.Item["ShipDate"]  = WrkDate.ToString('MMM - dd - yyyy').ToUpper()
							Endif
						Else
							ItemPosRow.Item["ShipDate"]  = POHSDT
						Endif
						
						If POHODT <> *Zero
							Wrkdate = POHODT
							If DateFormat = 'DMY'
								ItemPosRow.Item["OrderDate"]  = WrkDate.ToString('dd - MMM - yyyy').ToUpper()
							Else
								ItemPosRow.Item["OrderDate"]  = WrkDate.ToString('MMM - dd - yyyy').ToUpper()
							Endif
						Else
							ItemPosRow.Item["OrderDate"]  = POHODT  
						Endif
											
						ItemPosRow.Item["Market"]     = POHMKT
						ItemPosRow.Item["Store"]      = POHSTR
						ItemPosRow.Item["Status"]     = POHSTS
						ItemPosRow.Item["Qty"]        = QtyCalc
						ItemPosRow.Item["Cost"]       = CostCalc
						ItemPosRow.Item["Currency"]   = POHCYC
						ItemPosTbl.Rows.Add(ItemPosRow)
					Endif
				Endif
			Endif
		Enddo
						
		LeaveSr ItemPosTbl
	EndFunc
	
	//Create the Item Pos Table
	BegSr CreateItemPosTbl Access(*Private)
		
		ItemPosTbl = *New DataTable("ItemPosTbl")
								
		//Create Spice PO# Column
		ItemPosClm = *New DataColumn('SpicePO')
		ItemPosClm.DataType = System.Type.GetType("System.String")
		ItemPosClm.ColumnName = "SpicePO"
		ItemPosTbl.Columns.Add(ItemPosClm)
		
		//Create Revision Column
		ItemPosClm = *New DataColumn('Revision')
		ItemPosClm.DataType = System.Type.GetType("System.String")
		ItemPosClm.ColumnName = "Revision"
		ItemPosTbl.Columns.Add(ItemPosClm)
		
		//Create IP PO# Column
		ItemPosClm = *New DataColumn('IPPO')
		ItemPosClm.DataType = System.Type.GetType("System.String")
		ItemPosClm.ColumnName = "IPPO"
		ItemPosTbl.Columns.Add(ItemPosClm)
		
		//Create Vendor Column
		ItemPosClm = *New DataColumn('Vendor')
		ItemPosClm.DataType = System.Type.GetType("System.String")
		ItemPosClm.ColumnName = "Vendor"
		ItemPosTbl.Columns.Add(ItemPosClm)
		
		//Create Vendor Name Column
		ItemPosClm = *New DataColumn('VendorName')
		ItemPosClm.DataType = System.Type.GetType("System.String")
		ItemPosClm.ColumnName = "VendorName"
		ItemPosTbl.Columns.Add(ItemPosClm)
		
		//Create Ship Date Column
		ItemPosClm = *New DataColumn('ShipDate')
		ItemPosClm.DataType = System.Type.GetType("System.String")
		ItemPosClm.ColumnName = "ShipDate"
		ItemPosTbl.Columns.Add(ItemPosClm)
		
		//Create Order Date Column
		ItemPosClm = *New DataColumn('OrderDate')
		ItemPosClm.DataType = System.Type.GetType("System.String")
		ItemPosClm.ColumnName = "OrderDate"
		ItemPosTbl.Columns.Add(ItemPosClm)
		
		//Create Market Column
		ItemPosClm = *New DataColumn('Market')
		ItemPosClm.DataType = System.Type.GetType("System.String")
		ItemPosClm.ColumnName = "Market"
		ItemPosTbl.Columns.Add(ItemPosClm)
		
		//Create Store Column
		ItemPosClm = *New DataColumn('Store')
		ItemPosClm.DataType = System.Type.GetType("System.String")
		ItemPosClm.ColumnName = "Store"
		ItemPosTbl.Columns.Add(ItemPosClm)
		
		//Create Status Column
		ItemPosClm = *New DataColumn('Status')
		ItemPosClm.DataType = System.Type.GetType("System.String")
		ItemPosClm.ColumnName = "Status"
		ItemPosTbl.Columns.Add(ItemPosClm)
		
		//Create Qty Column
		ItemPosClm = *New DataColumn('Qty')
		ItemPosClm.DataType = System.Type.GetType("System.Int32")
		ItemPosClm.ColumnName = "Qty"
		ItemPosTbl.Columns.Add(ItemPosClm)
		
		//Create Cost Column
		ItemPosClm = *New DataColumn('Cost')
		ItemPosClm.DataType = System.Type.GetType("System.Decimal")
		ItemPosClm.ColumnName = "Cost"
		ItemPosTbl.Columns.Add(ItemPosClm)
		
		//Create Currency Column
		ItemPosClm = *New DataColumn('Currency')
		ItemPosClm.DataType = System.Type.GetType("System.String")
		ItemPosClm.ColumnName = "Currency"
		ItemPosTbl.Columns.Add(ItemPosClm)
	EndSr
	/EndRegion
EndClass