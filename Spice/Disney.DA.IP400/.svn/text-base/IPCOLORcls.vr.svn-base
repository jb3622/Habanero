Using System
Using System.Text
Using System.Data
Using System.IO

BegClass IPCOLORcls Access(*Public)
		
	DclDB Name(PgmDB) DbName("TDST")
	DclDiskFile Name(DSCOLORJ) Type(*Input) Org(*Indexed) ImpOpen(*No) DB(PgmDB) AddRec(*No)  NetBlockFactor(400)
	
	DclFld ColourTbl Type(DataTable)
	DclFld pXMLpath  Type(String)

	BegConstructor Access(*Public)
		DclSrParm PgmDB Type(ASNA.VisualRPG.Runtime.Database)
		DclSrParm Environment Type(Disney.Menu.Environments)
		
		*this.PgmDB = PgmDB
		
		pXMLpath = Path.Combine(Environment.PathToApplicationXML,"Colour.xml")
		LoadDataTableFromXML()
	EndConstructor
	
	DclFld pColourCode Type(Int16) Access(*Private)
	BegProp ColourCode Type(Int16) Access(*Public)
		BegGet
			LeaveSR pColourCode
		EndGet
	EndProp
	
	DclFld pColourName Type(String) Access(*Private)
	BegProp ColorName Type(String) Access(*Public)
		BegGet
			LeaveSR pColourName
		EndGet
	EndProp
	
	DclFld pStatus Type(Boolean) Access(*Private)
	BegProp Status Type(Boolean) Access(*Public)
		BegGet
			LeaveSR pStatus
		EndGet
	EndProp	
	
	BegSr LoadDataTableFromXML Access(*Private)
			
		If ColourTbl = *Nothing
			CreateTbl()
			If Not File.Exists(pXMLpath)
				CreateColourXML()
			Else			
				ColourTbl.ReadXml(pXMLpath)
			EndIf
		EndIf	
	EndSr
	
	BegSr CreateTbl Access(*Private)
	
		DclArray Keys Type(DataColumn) Rank(1)

		ColourTbl = *New DataTable("Colour")

		ColourTbl.Columns.Add(*New DataColumn("ColourCode", *TypeOf(Int16)))
		ColourTbl.Columns["ColourCode"].ReadOnly = *True

		ColourTbl.Columns.Add(*New DataColumn("ColourName", *TypeOf(String)))
		ColourTbl.Columns["ColourName"].ReadOnly = *True
		
		ColourTbl.Columns.Add(*New DataColumn("Status", *TypeOf(Boolean)))
		ColourTbl.Columns["Status"].ReadOnly = *True
		
		Keys = *New DataColumn[] {ColourTbl.Columns("ColourCode")}
		ColourTbl.PrimaryKey = Keys
	EndSr
	
	BegSr CreateColourXML Access(*Private)
	
		DclFld dr Type(DataRow)
			
		If Not DSCOLORJ.IsOpen
			Open DSCOLORJ
		EndIf
		
		DoUntil DSCOLORJ.IsEof
			Read DSCOLORJ
			If Not DSCOLORJ.IsEof
				dr = ColourTbl.NewRow()
				
				dr.Item["ColourCode"] = CCLR
				dr.Item["ColourName"] = CLRN.Trim()
				
				If CSTS = "Y"
					dr.Item["Status"] = *True
				Else
					dr.Item["Status"] = *False
				EndIf
				
				ColourTbl.Rows.Add(dr)
			EndIf
		EndDo
		
		ColourTbl.WriteXml(pXMLpath)
		Close DSCOLORJ
	EndSr	
	
	BegFunc GetColour Type(Boolean) Access(*Public)
		DclSrParm Colour Type(String)
		
		DclFld dr Type(DataRow)
		
		dr = ColourTbl.Rows.Find(Colour)
		If dr <> *Nothing
			pColourCode = dr.Item("ColourCode") *As Int16
			pColourName = dr.Item["ColourName"] *As String
			pStatus     = dr.Item["Status"]     *As Boolean
			
			LeaveSR *True
		Else
			LeaveSR *False
		EndIf
	EndFunc
	
	BegFunc GetColourTbl Type(DataTable) Access(*Public)
		
		LeaveSR ColourTbl
	EndFunc
EndClass