Using System
Using System.Text
Using System.Data

BegClass DSpoSND Access(*Public)

	DclDB Name(PgmDB) DbName("TDST")
	DclDiskFile Name(DSPOSND) Type(*Input) Org(*Indexed) ImpOpen(*No) DB(PgmDB)

	BegConstructor Access(*Public)
		DclSrParm PgmDB Type(ASNA.VisualRPG.Runtime.Database)

		*this.PgmDB = PgmDB
	EndConstructor
	
	BegFunc CreateTbl Type(DataTable) Access(*Private)
	
		DclFld POheaderTbl Type(DataTable)
	
		POheaderTbl = *New DataTable("EDIhdr")
		
		POheaderTbl.Columns.Add(*New DataColumn("POnumber",*TypeOf(String)))
		POheaderTbl.Columns.Add(*New DataColumn("SCBdate", *TypeOf(DateTime)))

		LeaveSR POheaderTbl
	EndFunc

	BegFunc GetEDIdata Type(DataTable) Access(*Public)
		DclSrParm POnumber Type(String)
		
		DclFld POheaderTbl Type(DataTable)
		DclFld dr          Type(DataRow)
		
		If Not DSPOSND.IsOpen
			Open DSPOSND
		EndIf
		
		Chain From(DSPOSND) Key(POnumber)
		If DSPOSND.IsFound
			POheaderTbl = CreateTbl()
		
			dr = POheaderTbl.NewRow()
			
			dr["POnumber"] = DHPONO
			dr["SCBdate"]  = CvtMDYdateToDateTime(DHTRD)

			POheaderTbl.Rows.Add(dr)
			
			Close DSPOSND
			LeaveSR POheaderTbl
		Else
			Close DSPOSND
			LeaveSR *Nothing
		EndIf
	EndFunc
	
	BegFunc CvtMDYdateToDateTime Type(DateTime) Access(*Private)
		DclSrParm Date Type(*Zoned) Len(8,0)
	
		DclFld StrDate Type(String)
		DclFld Year    Type(Int32)
		DclFld Month   Type(Int32)
		DclFld Day     Type(Int32)
		
		If Date <> *Zero
			StrDate = Date.ToString('00000000')
			Year    = Convert.ToInt32(StrDate.SubString(4,4))
			Month   = Convert.ToInt32(StrDate.SubString(0,2))
			Day     = Convert.ToInt32(StrDate.SubString(2,2))
			
			LeaveSR (*New DateTime(Year, Month, Day, 0, 0, 0))
		Else
			LeaveSR DateTime.MinValue
		EndIf
	EndFunc
EndClass