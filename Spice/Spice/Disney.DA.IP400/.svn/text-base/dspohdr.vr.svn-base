Using System
Using System.Text
Using System.Data

BegClass dspohdr Access(*Public)

	DclDB Name(PgmDB) DbName("TDST")
	DclDiskFile Name(DSPOHDRA) Type(*Input) Org(*Indexed) ImpOpen(*No) DB(PgmDB)
	
	BegConstructor Access(*Public)
		DclSrParm PgmDB Type(ASNA.VisualRPG.Runtime.Database)
		
		*this.PgmDB = PgmDB
	EndConstructor
	
	BegFunc CreateTbl Type(DataTable) Access(*Private)
	
		DclFld POheaderTbl Type(DataTable)
	
		POheaderTbl = *New DataTable("POheader")
		
		POheaderTbl.Columns.Add(*New DataColumn("POnumber", *TypeOf(String)))
		POheaderTbl.Columns.Add(*New DataColumn("OOCLdate", *TypeOf(DateTime)))
		POheaderTbl.Columns.Add(*New DataColumn("AveryDate",*TypeOf(DateTime)))

		LeaveSR POheaderTbl
	EndFunc
	
	BegFunc GetHeader Type(DataTable) Access(*Public)
		DclSrParm POnumber Type(String)
		
		DclFld POheaderTbl Type(DataTable)
		DclFld dr          Type(DataRow)
		
		If Not DSPOHDRA.IsOpen
			DSPOHDRA.MemberName = "DSPOHDR"
			Open DSPOHDRA
		EndIf
		
		Chain From(DSPOHDRA) Key(POnumber)
		If DSPOHDRA.IsFound
			POheaderTbl = CreateTbl()
		
			dr = POheaderTbl.NewRow()
			
			dr["POnumber"]  = DHONO
			dr["OOCLdate"]  = CvtToDateTime(DHXMTL)
			dr["AveryDate"] = CvtToDateTime(DHCAST)

			POheaderTbl.Rows.Add(dr)
			
			Close DSPOHDRA
			LeaveSR POheaderTbl
		Else
			Close DSPOHDRA
			LeaveSR *Nothing
		EndIf
	EndFunc
	
	BegFunc CvtToDateTime Type(DateTime) Access(*Private)
		DclSrParm Date Type(*Zoned) Len(8,0)
	
		DclFld StrDate Type(String)
		DclFld Year    Type(Int32)
		DclFld Month   Type(Int32)
		DclFld Day     Type(Int32)
		
		If Date <> *Zero
			StrDate = Date.ToString('00000000')
			Year    = Convert.ToInt32(StrDate.SubString(0,4))
			Month   = Convert.ToInt32(StrDate.SubString(4,2))
			Day     = Convert.ToInt32(StrDate.SubString(6,2))
			
			LeaveSR (*New DateTime(Year, Month, Day, 0, 0, 0))
		Else
			LeaveSR DateTime.MinValue
		EndIf
	EndFunc
EndClass