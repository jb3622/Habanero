Using System
Using System.Text
Using System.IO
Using System.Data
Using System.Xml

BegClass AgeGroup Access(*Public)

	DclDB PgmDB DBName("TDST")
	DclDiskFile Name(DSSPAGP) Type(*Input) Org(*Arrival) DB(PgmDB) ImpOpen(*No)
	
	DclFld AgeGroupTbl  Type(DataTable)
	DclFld pAgeGroupID  Type(String)
	DclFld pDescription Type(String)
	DclFld pXMLpath     Type(String)

	BegConstructor Access(*Public)
		DclSrParm PgmDB       Type(ASNA.VisualRPG.Runtime.Database)
		DclSrParm Environment Type(Disney.Menu.Environments)
		
		*this.PgmDB = PgmDB
		
		pXMLpath = Path.Combine(Environment.PathToApplicationXML,"AgeGroup.xml")
		LoadDataTableFromXml()
	EndConstructor
	
	/Region Properties
	BegProp AgeGroupID Type(String) Access(*Public)
		BegGet
			LeaveSR pAgeGroupID
		EndGet
	EndProp
	
	BegProp AgeGroupDescription Type(String) Access(*Public)
		BegGet
			LeaveSR pDescription
		EndGet
	EndProp
	/EndRegion
	
	BegSr LoadDataTableFromXml Access(*Private)
			
		If AgeGroupTbl = *Nothing
			CreateTbl()
			If Not File.Exists(pXMLpath)
				PopulateDataTable()
			Else
				AgeGroupTbl.ReadXml(pXMLpath)
			EndIf
		EndIf
	EndSr

	BegSr CreateTbl Access(*Private)
	
		DclArray Keys    Type(DataColumn) Rank(1)

		AgeGroupTbl = *New DataTable("AgeGroups")

		AgeGroupTbl.Columns.Add(*New DataColumn("AgeGroupID", *TypeOf(String)))
		AgeGroupTbl.Columns["AgeGroupID"].ReadOnly = *True

		AgeGroupTbl.Columns.Add(*New DataColumn("Description", *TypeOf(String)))
		AgeGroupTbl.Columns["Description"].ReadOnly = *True

		AgeGroupTbl.Columns.Add(*New DataColumn("GroupDescription", *TypeOf(String)))
		AgeGroupTbl.Columns["GroupDescription"].ReadOnly = *True
		
		Keys = *New DataColumn[] {AgeGroupTbl.Columns("AgeGroupID")}
		AgeGroupTbl.PrimaryKey = Keys
	EndSr

	BegSr PopulateDataTable Access(*Private)
	
		DclFld dr Type(DataRow)
		
		If Not DSSPAGP.IsOpen
			Open DSSPAGP
		EndIf
				
		DoUntil DSSPAGP.IsEof
			Read DSSPAGP
			If Not DSSPAGP.IsEof
				dr = AgeGroupTbl.NewRow()
				
				dr.Item("AgeGroupID")  = AGPAGI
				dr.Item["Description"] = AGPAGD.Trim()
				dr.Item["GroupDescription"] = AGPAGI + " - " + AGPAGD

				AgeGroupTbl.Rows.Add(dr)
			EndIf
		EndDo
		
		AgeGroupTbl.WriteXml(pXMLpath)
		
		Close DSSPAGP
	EndSr

	BegFunc GetAgeGroup Type(*Boolean) Access(*Public)
		DclSrParm AgeGroup Type(String)

		DclFld dr Type(DataRow)
		
		dr = AgeGroupTbl.Rows.Find(AgeGroup)
		If dr <> *Nothing
			pAgeGroupID  = dr.Item("AgeGroupID")  *As String
			pDescription = dr.Item("Description") *As String
			
			LeaveSR *True
		Else
			LeaveSR *False
		EndIf
	EndFunc
	
	BegFunc GetAgeGroupTbl Type(DataTable) Access(*Public)
		
		LeaveSR AgeGroupTbl
	EndFunc
EndClass