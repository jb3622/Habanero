Using System
Using System.Text
Using System.Data
Using System.IO

BegClass DSSPIVR Access(*Public)

	DclDB Name(PgmDB) DBName("TDST")
	DclDiskFile Name(DSSPIVR) Type(*Input) Org(*Arrival) DB(PgmDB) ImpOpen(*No)
	
	DclFld VATrates          Type(DataTable)
	DclFld pSalesVATrate     Type(Decimal)
	DclFld pPurchasesVATrate Type(Decimal)
	DclFld pXMLpath          Type(String)
	
	/Region Properties
	BegProp SalesVATrate Type(Decimal) Access(*Public)
		BegGet
			LeaveSR pSalesVATrate
		EndGet
	EndProp
	
	BegProp PurchasesVATrate Type(Decimal) Access(*Public)
		BegGet
			LeaveSR pPurchasesVATrate
		EndGet
	EndProp
	/EndRegion
	
	BegConstructor Access(*Public)
		DclSrParm PgmDB Type(ASNA.VisualRPG.Runtime.Database)
		DclSrParm Environment Type(Disney.Menu.Environments)
		
		*This.PgmDB = PgmDB
		
		pXMLpath = Path.Combine(Environment.PathToApplicationXML,"VATrates.xml")
		LoadDataTableFromXML()
	EndConstructor
	
	BegSr LoadDataTableFromXML Access(*Private)			
		If VATrates = *Nothing
			CreateTbl()
			If Not File.Exists(pXMLpath)
				PopulateDataTable()
			Else
				VATrates.ReadXml(pXMLpath)
			EndIf
		EndIf
	EndSr
	
	BegSr CreateTbl Access(*Private)
	
		DclArray Keys Type(DataColumn) Rank(1)

		VATrates = *New DataTable("VATrates")

		VATrates.Columns.Add(*New DataColumn("Market", *TypeOf(String)))
		VATrates.Columns["Market"].ReadOnly = *True

		VATrates.Columns.Add(*New DataColumn("StoreVATcode", *TypeOf(String)))
		VATrates.Columns["StoreVATcode"].ReadOnly = *True
		
		VATrates.Columns.Add(*New DataColumn("ItemVATcode", *TypeOf(String)))
		VATrates.Columns["ItemVATcode"].ReadOnly = *True
		
		VATrates.Columns.Add(*New DataColumn("SalesVATrate", *TypeOf(Decimal)))
		VATrates.Columns["SalesVATrate"].ReadOnly = *True
		
		VATrates.Columns.Add(*New DataColumn("PurchasesVATrate", *TypeOf(Decimal)))
		VATrates.Columns["PurchasesVATrate"].ReadOnly = *True
		
		Keys = *New DataColumn[] {VATrates.Columns("Market"),VATrates.Columns("StoreVATcode"),VATrates.Columns("ItemVATcode")}
		VATrates.PrimaryKey = Keys
	EndSr
	
	BegSr PopulateDataTable Access(*Private)
	
		DclFld dr Type(DataRow)
		
		If Not DSSPIVR.IsOpen
			Open DSSPIVR
		EndIf
		
		DoUntil DSSPIVR.IsEof
			Read DSSPIVR
			If Not DSSPIVR.IsEof

				dr = VATrates.NewRow()
				
				dr.Item("Market")           = IVRMKT
				dr.Item["StoreVATcode"]     = IVRSVC
				dr.Item["ItemVATcode"]      = IVRIVC
				dr.Item["SalesVATrate"]     = IVRSVR
				dr.Item["PurchasesVATrate"] = IVRPVR
				
				VATrates.Rows.Add(dr)
			EndIf
		EndDo
		
		VATrates.WriteXml(pXMLpath)
		
		Close DSSPIVR
	EndSr
	
	BegFunc GetVATrate Type(*Boolean) Access(*Public)
		DclSrParm Market       Type(String)
		DclSrParm StoreVATcode Type(String)
		DclSrParm ItemVATcode  Type(String)

		DclFld   dr   Type(DataRow)
		DclArray Keys Type(Object) Rank(1)
		
		Keys = *New Object[3]
		Keys[0] = Market
		Keys[1] = StoreVATcode
		Keys[2] = ItemVATcode
		
		dr = VATrates.Rows.Find(Keys)
		If dr <> *Nothing

			pSalesVATrate     = dr.Item["SalesVATrate"]     *As Decimal
			pPurchasesVATrate = dr.Item["PurchasesVATrate"] *As Decimal
			
			LeaveSR *True
		Else
			LeaveSR *False
		EndIf
	EndFunc
	
	BegFunc GetVATratesTbl Type(DataTable) Access(*Public)
		
		LeaveSR VATrates
	EndFunc
EndClass