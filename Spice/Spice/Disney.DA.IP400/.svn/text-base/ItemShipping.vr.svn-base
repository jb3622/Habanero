Using System
Using System.Data
Using System.IO
Using System.Text
Using System.Xml

BegClass ItemShipping Access(*Public)

	DclDB PgmDB DBName("TDST")
	DclDiskFile Name(DSSPSHA) Type(*Input) Org(*Indexed) DB(PgmDB) ImpOpen(*No)

	DclFld ItemShippingOptions    Type(DataTable) Access(*Private)
	DclFld pXMLpath  Type(String)    Access(*Private)

	BegConstructor Access(*Public)
		DclSrParm PgmDB Type(ASNA.VisualRPG.Runtime.Database)
		DclSrParm Environment Type(Disney.Menu.Environments)
		
		*this.PgmDB = PgmDB
		
		pXMLpath = Path.Combine(Environment.PathToApplicationXML,"ItemShipping.xml")
		LoadDataTableFromXML()
	EndConstructor
	
	BegSr LoadDataTableFromXML Access(*Private)
			
		If ItemShippingOptions = *Nothing
			CreateTbl()
			If Not File.Exists(pXMLpath)
				PopulateDataTable()
			Else
				ItemShippingOptions.ReadXml(pXMLpath)
			EndIf
		EndIf
	EndSr

	BegSr CreateTbl Access(*Private)
	
		DclArray Keys    Type(DataColumn) Rank(1)

		ItemShippingOptions = *New DataTable("ItemShippingTable")

		ItemShippingOptions.Columns.Add(*New DataColumn("ItemShippingCode", *TypeOf(String)))
		ItemShippingOptions.Columns["ItemShippingCode"].ReadOnly = *True

		ItemShippingOptions.Columns.Add(*New DataColumn("Description", *TypeOf(String)))
		ItemShippingOptions.Columns["Description"].ReadOnly = *True

		ItemShippingOptions.Columns.Add(*New DataColumn("CodeAndDescription", *TypeOf(String)))
		ItemShippingOptions.Columns["CodeAndDescription"].ReadOnly = *True
				
		Keys = *New DataColumn[] {ItemShippingOptions.Columns("ItemShippingCode")}
		ItemShippingOptions.PrimaryKey = Keys
	EndSr

	BegSr PopulateDataTable Access(*Private)
	
		DclFld dr Type(DataRow)
		
		If Not DSSPSHA.IsOpen
			Open DSSPSHA
		EndIf
		
		DoUntil DSSPSHA.IsEof
			Read DSSPSHA
			If Not DSSPSHA.IsEof

				dr = ItemShippingOptions.NewRow()
				
				dr.Item["ItemShippingCode"] = SHACOD
				dr.Item["ItemShippingCode"] = dr.Item["ItemShippingCode"].ToString().Trim()
				dr.Item["Description"]      = SHADES
				dr.Item["Description"]      = dr.Item["Description"].ToString().Trim()
				dr.Item["CodeAndDescription"] = dr.Item["ItemShippingCode"].ToString() + " - " + dr.Item["Description"].ToString()
												
				ItemShippingOptions.Rows.Add(dr)
			EndIf
		EndDo
		
		ItemShippingOptions.WriteXml(pXMLpath)
		
		Close DSSPSHA

	EndSr

	BegFunc GetItemShippingOptions Type(DataTable) Access(*Public)
		
		LeaveSR ItemShippingOptions
	EndFunc
EndClass