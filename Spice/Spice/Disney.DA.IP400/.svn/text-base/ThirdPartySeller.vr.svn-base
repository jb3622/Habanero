Using System
Using System.Data
Using System.IO
Using System.Text
Using System.Xml

BegClass ThirdPartySeller Access(*Public)

	DclDB PgmDB DBName("TDST")
	DclDiskFile Name(DSSPTPS) Type(*Input) Org(*Indexed) DB(PgmDB) ImpOpen(*No)

	DclFld ThirdPartySellerTbl    Type(DataTable) Access(*Private)
	DclFld pXMLpath  Type(String)    Access(*Private)

	BegConstructor Access(*Public)
		DclSrParm PgmDB Type(ASNA.VisualRPG.Runtime.Database)
		DclSrParm Environment Type(Disney.Menu.Environments)
		
		*this.PgmDB = PgmDB
		
		pXMLpath = Path.Combine(Environment.PathToApplicationXML,"ThirdPartySeller.xml")
		LoadDataTableFromXML()
	EndConstructor
	
	BegSr LoadDataTableFromXML Access(*Private)
			
		If ThirdPartySellerTbl = *Nothing
			CreateTbl()
			If Not File.Exists(pXMLpath)
				PopulateDataTable()
			Else
				ThirdPartySellerTbl.ReadXml(pXMLpath)
			EndIf
		EndIf
	EndSr

	BegSr CreateTbl Access(*Private)
	
		DclArray Keys    Type(DataColumn) Rank(1)

		ThirdPartySellerTbl = *New DataTable("ThirdPartySellerTable")

		ThirdPartySellerTbl.Columns.Add(*New DataColumn("ThirdPartySellerCode", *TypeOf(String)))
		ThirdPartySellerTbl.Columns["ThirdPartySellerCode"].ReadOnly = *True

		ThirdPartySellerTbl.Columns.Add(*New DataColumn("Description", *TypeOf(String)))
		ThirdPartySellerTbl.Columns["Description"].ReadOnly = *True

		ThirdPartySellerTbl.Columns.Add(*New DataColumn("CodeAndDescription", *TypeOf(String)))
		ThirdPartySellerTbl.Columns["CodeAndDescription"].ReadOnly = *True

		ThirdPartySellerTbl.Columns.Add(*New DataColumn("Default", *TypeOf(String)))
		ThirdPartySellerTbl.Columns["Default"].ReadOnly = *True
			
		Keys = *New DataColumn[] {ThirdPartySellerTbl.Columns("ThirdPartySellerCode")}
		ThirdPartySellerTbl.PrimaryKey = Keys
	EndSr

	BegSr PopulateDataTable Access(*Private)
	
		DclFld dr Type(DataRow)
		
		If Not DSSPTPS.IsOpen
			Open DSSPTPS
		EndIf
		
		DoUntil DSSPTPS.IsEof
			Read DSSPTPS
			If Not DSSPTPS.IsEof

				dr = ThirdPartySellerTbl.NewRow()
				
				dr.Item["ThirdPartySellerCode"] = TPSCOD
				dr.Item["ThirdPartySellerCode"] = dr.Item["ThirdPartySellerCode"].ToString().Trim()
				dr.Item["Description"]          = TPSDES
				dr.Item["Description"]          = dr.Item["Description"].ToString().Trim()
				dr.Item["CodeAndDescription"]   = dr.Item["ThirdPartySellerCode"].ToString() + " - " + dr.Item["Description"].ToString()
				dr.Item["Default"]              = TPSDFT
								
				ThirdPartySellerTbl.Rows.Add(dr)
			EndIf
		EndDo
		
		ThirdPartySellerTbl.WriteXml(pXMLpath)
		
		Close DSSPTPS

	EndSr

	BegFunc GetThirdPartySellerTbl Type(DataTable) Access(*Public)
		
		LeaveSR ThirdPartySellerTbl
	EndFunc
EndClass