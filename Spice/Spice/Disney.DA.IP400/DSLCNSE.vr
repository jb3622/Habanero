Using System
Using System.Text
Using System.Data
Using System.IO

BegClass DSLCNSE Access(*Public)

	DclDB Name(PgmDB) DBName("TDST")
	DclDiskFile Name(DSLCNSE) DB(PgmDB) Org(*Indexed) Type(*Input) ImpOpen(*No)

	DclFld LicenceTbl Type(DataTable)   Access(*Private)
	DclArray Keys Type(DataColumn) Rank(1) Access(*Private)
	DclFld pLicenceCode   Type(String)  Access(*Private)
	DclFld pLicenceDesc   Type(String)  Access(*Private)
	DclFld pLicenceStatus Type(String)  Access(*Private)
	DclFld dr             Type(DataRow) Access(*Private)
	DclFld pXMLpath       Type(String)  Access(*Private)

	BegConstructor Access(*Public)
		DclSrParm PgmDB Type(ASNA.VisualRPG.Runtime.Database)
		DclSrParm Environment Type(Disney.Menu.Environments)
		
		*this.PgmDB = PgmDB
		
		pXMLpath = Path.Combine(Environment.PathToApplicationXML,"Licence.xml")
		LoadDataTableFromXML()
	EndConstructor
	
	BegProp LicenceCode Type(String) Access(*Public)
		BegGet
			LeaveSR pLicenceCode
		EndGet
	EndProp
	
	BegProp LicenceDescription Type(String) Access(*Public)
		BegGet
			LeaveSR pLicenceDesc
		EndGet
	EndProp
	
	BegProp LicenceStaus Type(String) Access(*Public)
		BegGet
			LeaveSR pLicenceStatus
		EndGet
	EndProp
	
	BegSR LoadDataTableFromXML Access(*Private)
	
		If LicenceTbl = *Nothing
			CreateTbl()
			If Not File.Exists(pXMLpath)
				CreateLicenceXML()
			Else
				LicenceTbl.ReadXml(pXMLpath)
			EndIf
		EndIf	
	EndSR
	
	BegSr CreateTbl Access(*Private)
		
		LicenceTbl = *New DataTable("Licence")

		LicenceTbl.Columns.Add(*New DataColumn("Code", *TypeOf(String)))
		LicenceTbl.Columns["Code"].ReadOnly = *True

		LicenceTbl.Columns.Add(*New DataColumn("Description", *TypeOf(String)))
		LicenceTbl.Columns["Description"].ReadOnly = *True
		
		LicenceTbl.Columns.Add(*New DataColumn("CodeDescription", *TypeOf(String)))
		LicenceTbl.Columns("CodeDescription").Expression = "Code + ' - ' + Description"
		LicenceTbl.Columns["CodeDescription"].ReadOnly = *True
		
		LicenceTbl.Columns.Add(*New DataColumn("Status", *TypeOf(String)))
		LicenceTbl.Columns["Status"].ReadOnly = *True
		
		Keys = *New DataColumn[] {LicenceTbl.Columns("Code")}
		LicenceTbl.PrimaryKey = Keys
	EndSr
	
	BegSr CreateLicenceXML Access(*Private)
			
		If Not DSLCNSE.IsOpen
			Open DSLCNSE
		EndIf
		
		DoUntil DSLCNSE.IsEof
			Read DSLCNSE
			If Not DSLCNSE.IsEof
				dr = LicenceTbl.NewRow()
				dr.Item["Code"]        = LCCODE
				dr.Item["Description"] = LCDESC.Trim()
				dr.Item["Status"]      = LCSTS
				LicenceTbl.Rows.Add(dr)
			EndIf
		EndDo
		
		LicenceTbl.WriteXml(pXMLpath)
		
		Close DSLCNSE
	EndSr

	BegFunc GetLicence Type(*Boolean) Access(*Public)
		DclSrParm Licence Type(String)

		DclFld dr Type(DataRow)
		
		If LicenceTbl = *Nothing
			LoadDataTableFromXML()
		EndIf
		
		dr = LicenceTbl.Rows.Find(Licence)
		If dr <> *Nothing
			pLicenceCode   = dr.Item("Code")        *As String
			pLicenceDesc   = dr.Item["Description"] *As String
			pLicenceStatus = dr.Item["Status"]      *As String
			LeaveSR *True
		Else
			LeaveSR *False
		EndIf
	EndFunc
	
	BegFunc GetLicenceTbl Type(DataTable) Access(*Public)
		
		LeaveSR LicenceTbl
	EndFunc
EndClass