// ===============================================
// Tarnjit Bhambra
// 08 April 2009
// DSSPAPPcls	
// Data Access Layer - Applications
//------------------------------------------------
// Modification
//================================================

Using System
Using System.Text

BegClass DSSPAPPcls Access(*Public)

	DclFld memRecords Type(*Integer4)  Access(*Public)
	DclFld NxtApplicationID Type(*Zoned) Len(5,0) Access(*Public)
	
	DclDB Name(DB) DbName("TDST")
	
	DclDiskFile Name(DSSPAPPL1) Type(*Update) Org(*Indexed) ImpOpen(*No) DB(DB) AddRec(*Yes) RnmFmt(Dsspapr:R_Dsspapr)
	
	DclMemoryFile Name(memDSSPAPP) DBdesc(DB) Filedesc('DSSPAPP') +
		RnmFmt(RDSSPAPP, mDSSPAPP) ImpOpen(*No)
	
	BegConstructor Access(*Public)
		DclSrParm DB Type(ASNA.VisualRPG.Runtime.Database)
		
		*this.DB = DB
	EndConstructor
	
	BegConstructor Access(*Public)
		DclSrParm DB    Type(ASNA.VisualRPG.Runtime.Database)
		DclSrParm ApplicationID Type(*Zoned) Len(5,0)
		
		*this.DB = DB
		
		GetApplicationID(ApplicationID)
	EndConstructor
	
		
	//Properties
	BegProp Next_Application_ID Type(*Zoned) Len(5,0) Access(*Public)
		BegGet
			LeaveSR NxtApplicationID
		EndGet
	EndProp
		
	BegProp Application_ID Type(*Zoned) Len(5,0) Access(*Public)
		BegGet
			LeaveSR APPID
		EndGet
	EndProp
	
	BegProp Application_Name Type(*Char) Len(35) Access(*Public)
		BegGet
			LeaveSR APPNAM
		EndGet
		
		BegSet
			APPNAM = *PropVal
		EndSet
	EndProp
	
	BegProp Create_Date Type(*Zoned ) Len(8,0) Access(*Public)
		BegGet
			LeaveSR APPCRD
		EndGet
		
		BegSet
			APPCRD = *PropVal
		EndSet
	EndProp
	
	BegProp Create_Time Type(*Zoned ) Len(6,0) Access(*Public)
		BegGet
			LeaveSR APPCRT
		EndGet
		
		BegSet
			APPCRT = *PropVal
		EndSet
	EndProp
	
	
	BegProp Create_User Type(*Char ) len(10) Access(*Public)
		BegGet
			LeaveSR APPCRU 
		EndGet
		
		BegSet
			APPCRU = *PropVal
		EndSet
	EndProp
	
		BegProp Changed_Date Type(*Zoned ) Len(8,0) Access(*Public)
		BegGet
			LeaveSR APPCHD
		EndGet
		
		BegSet
			APPCHD = *PropVal
		EndSet
	EndProp
	
	BegProp Changed_Time Type(*Zoned ) Len(6,0) Access(*Public)
		BegGet
			LeaveSR APPCHT 
		EndGet
		
		BegSet
			APPCHT  = *PropVal
		EndSet
	EndProp
	
	BegProp Changed_User Type(*Char ) len(10) Access(*Public)
		BegGet
			LeaveSR APPCHU 
		EndGet
		
		BegSet
			APPCHU  = *PropVal
		EndSet
	EndProp
	
	// dataSet record Count
	BegProp DataSet_Record_Count Type(*Integer4) Access(*Public)
		BegGet
			LeaveSR memRecords
		EndGet
	EndProp
	
		
	// Methods
	BegFunc GetApplicationID Type(*Boolean) Access(*Public)
		DclSrParm ApplicationID Type(*Zoned) len(5,0)
		
		If Not DSSPAPPL1.IsOpen
			Open DSSPAPPL1
		EndIf
		
		Chain From(DSSPAPPL1) Key(ApplicationID) Access(*NoLock)
		If %Found
			LeaveSR *Off
		Else
			LeaveSR *On
		EndIf
	EndFunc
	
	BegFunc GetApplicationIDForUpdate Type(*Boolean) Access(*Public)
		DclSrParm ApplicationID Type(*Zoned) len(5,0)
		
		If Not DSSPAPPL1.IsOpen
			Open DSSPAPPL1
		EndIf
		
		Chain From(DSSPAPPL1) Key(ApplicationID) Access(*DFT) Err(*Extended)
		If Not %Found
			LeaveSR *On
		EndIf
		
		If %Error = *On
			Throw *New System.Exception('Failed to get record for update (DSSPAPPL01)')
			LeaveSR *On
		EndIf
		
		LeaveSR *Off
	EndFunc
	
	
	BegFunc GetNextApplicationID  Type(*Zoned) Len(5,0)  Access(*Public)
		
		NxtApplicationID = *Hival
		
		If Not DSSPAPPL1.IsOpen
			Open DSSPAPPL1
		EndIf
		
		SetGT File(DSSPAPPL1) Key(NxtApplicationID)
		ReadP  DSSPAPPL1
		If Not %Eof(DSSPAPPL1)
			NxtApplicationID = APPID + 1
		  Else
		    NxtApplicationID = 1	
		Endif
		
		LeaveSR NxtApplicationID
	EndFunc
		
		
	BegSR WriteApplicationID Access(*Public)
		DclSrParm ApplicationID Type(*Zoned) len(5,0)
	
		If Not DSSPAPPL1.IsOpen
			Open DSSPAPPL1
		EndIf
		
		APPID = ApplicationID
		
		Write RDSSPAPP Err(*Extended)
		If %Error = *On
			Throw *New System.Exception('Failed to write record (DSSPAPPL1)')
		EndIf
		
		LeaveSR
	EndSR
	
	BegFunc UpdateApplicationID Type(*Boolean) Access(*Public) 
		DclSrParm ApplicationID Type(*Zoned) len(5,0)
		
		Update RDsspapp Err(*Extended)
		If %Error = *On
			Throw *New System.Exception('Failed to update record (DSSPAPPL1)')
			LeaveSR *True
		EndIf
		
		LeaveSR *Off
	EndFunc
	
	BegFunc DeleteApplicationID Type(*Boolean) Access(*Public)
		DclSrParm ApplicationID Type(*Zoned) len(5,0)
		
		If Not DSSPAPPL1.IsOpen
			Open DSSPAPPL1
		EndIf
		
		Delete From(RDsspapp) Key(ApplicationID) Err(*Extended)
		If %Error = *On
			LeaveSR *True
		EndIf
		
		LeaveSR *False
	EndFunc
	
	/Region Memory files
	BegFunc setMemDSSPAPP  Type(*Boolean) Access(*Public)
		
		If Not DSSPAPPL1.IsOpen
			Open DSSPAPPL1
		EndIf
	
	 	If Not memDSSPAPP.IsOpen
	 		Open memDSSPAPP
		Endif	
		
		Unlock DSSPAPPL1 
			
		memDSSPAPP.DataSet.Clear()
		memRecords = *Zero
	
		SetLL DSSPAPPL1 key(*Start)
		DoUntil DSSPAPPL1.IsEof
			Read DSSPAPPL1 Access(*NoLock)
			If (Not DSSPAPPL1.IsEof)
			   Write mDSSPAPP
			   memRecords += 1
		    Endif
		Enddo
	    
	    Close DSSPAPPL1 
	
		LeaveSR *True
	EndFunc	
	
	BegFunc ReadDSSPAPP Type(*Boolean) Access(*Public)
		DclSrParm wIdx Type(*Integer2)
		
		If wIdx > memRecords 
		   LeaveSR *True
		Endif   
		
		Chain memDSSPAPP key(wIdx)
		If memDSSPAPP.IsEof
		   Unlock mDSSPAPP
		   LeaveSR *True
		Endif
		
		LeaveSR *false   
	EndFunc
	/EndRegion
EndClass