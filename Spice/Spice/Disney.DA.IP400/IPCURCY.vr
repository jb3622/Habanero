Using System
Using System.Text
Using System.Data
Using System.IO
Using System.Xml

BegClass IPCURCY Access(*Public)

	DclDB Name(PgmDB) DbName("TDST")
	DclDiskFile Name(IPCURCY)  Type(*Input) Org(*Indexed) ImpOpen(*No) DB(PgmDB)
	
	DclFld   dr          Type(DataRow) Access(*Private)
	DclFld   CurrencyTbl Type(DataTable) Access(*Private)
	DclArray Keys        Type(DataColumn) Rank(1) Access(*Private)
	
	DclFld pCurrencyCode Type(String) Access(*Private)
	DclFld pCurrencyName Type(String) Access(*Private)
	DclFld pCurrencyRate Type(Decimal) Access(*Private)
	DclFld pXMLpath      Type(String)  Access(*Private)
	
	BegConstructor Access(*Public)
		DclSrParm PgmDB Type(ASNA.VisualRPG.Runtime.Database)
		DclSrParm Environment Type(Disney.Menu.Environments)
		
		*this.PgmDB = PgmDB
		
		pXMLpath = Path.Combine(Environment.PathToApplicationXML,"Currency.xml")
		LoadDataTableFromXML()
	EndConstructor
	
	/Region Properties
	BegProp CurrencyCode Type(String) Access(*Public)
		BegGet
			LeaveSR pCurrencyCode
		EndGet
	EndProp
	
	BegProp CurrencyName Type(String) Access(*Public)
		BegGet
			LeaveSR pCurrencyName
		EndGet
	EndProp
	
	BegProp CurrencyRate Type(Decimal) Access(*Public)
		BegGet
			LeaveSR pCurrencyRate
		EndGet
	EndProp
	/EndRegion
	
	BegSr LoadDataTableFromXML Access(*Private)
			
		If CurrencyTbl = *Nothing
			CreateTbl()
			If Not File.Exists(pXMLpath)
				CreateCurrencyXML()
			Else
				CurrencyTbl.ReadXml(pXMLpath)
			EndIf
		EndIf
	EndSr
	
	BegSr CreateTbl Access(*Private)
	
		DclFld dc Type(DataColumn)
	
		CurrencyTbl = *New DataTable("Currency")

		CurrencyTbl.Columns.Add(*New DataColumn("CurrencyCode", *TypeOf(String)))
		CurrencyTbl.Columns["CurrencyCode"].ReadOnly = *True

		CurrencyTbl.Columns.Add(*New DataColumn("CurrencyName", *TypeOf(String)))
		CurrencyTbl.Columns["CurrencyName"].ReadOnly = *True
		
		CurrencyTbl.Columns.Add(*New DataColumn("CurrencyRate", *TypeOf(Decimal)))
		CurrencyTbl.Columns["CurrencyRate"].ReadOnly = *True
				
		dc = *New DataColumn("CurrencyCodeName")
		dc.Expression = "CurrencyCode + ' - ' + CurrencyName"
		dc.ReadOnly = *True
		CurrencyTbl.Columns.Add(dc)
		
		Keys = *New DataColumn[] {CurrencyTbl.Columns("CurrencyCode")}
		CurrencyTbl.PrimaryKey = Keys
	EndSr

	BegSr CreateCurrencyXML Access(*Private)
			
		If Not IPCURCY.IsOpen
			Open IPCURCY
		EndIf
		
		DoUntil IPCURCY.IsEof
			Read IPCURCY
			If Not IPCURCY.IsEof
				dr = CurrencyTbl.NewRow()
				
				dr.Item["CurrencyCode"] = CCUR
				dr.Item["CurrencyName"] = CURN
				dr.Item["CurrencyRate"] = CVAL
				
				CurrencyTbl.Rows.Add(dr)
			EndIf
		EndDo
		
		CurrencyTbl.WriteXml(pXMLpath)
		
		Close IPCURCY
	EndSr
	
	BegFunc GetCurrency Type(*Boolean) Access(*Public)
		DclSrParm Currency Type(String)

		DclFld dr Type(DataRow)
		
		If CurrencyTbl = *Nothing
			LoadDataTableFromXML()
		EndIf
		
		dr = CurrencyTbl.Rows.Find(Currency)
		If dr <> *Nothing
			pCurrencyCode = dr.Item["CurrencyCode"] *As String
			pCurrencyName = dr.Item["CurrencyName"] *As String
			pCurrencyRate = dr.Item("CurrencyRate") *As Decimal
			
			LeaveSR *True
		Else
			LeaveSR *False
		EndIf
	EndFunc
	
	BegFunc GetCurrencyTbl Type(DataTable) Access(*Public)
		
		LeaveSR CurrencyTbl
	EndFunc
EndClass