Using System
Using System.Text
Using System.Xml
Using System.IO
Using System.Data

BegClass DSship Access(*Public)

	DclDB Name(PgmDB) DBName("TDST")
	DclDiskFile Name(DSship) Type(*Input) Org(*Arrival) DB(PgmDB) ImpOpen(*No)

	DclFld ShipViaTbl Type(DataTable) Access(*Private)
	DclFld pXMLpath   Type(String)    Access(*Private)
	
	BegConstructor Access(*Public)
		DclSrParm PgmDB Type(ASNA.VisualRPG.Runtime.Database)
		DclSrParm Environment Type(Disney.Menu.Environments)
		
		*this.PgmDB = PgmDB
		
		pXMLpath = Path.Combine(Environment.PathToApplicationXML,"ShipVia.xml")
		LoadDataTableFromXML()
	EndConstructor
	
	BegSr LoadDataTableFromXML Access(*Private)
			
		If ShipViaTbl = *Nothing
			CreateTbl()
			If Not File.Exists(pXMLpath)
				PopulateDataTable()
			Else
				ShipViaTbl.ReadXml(pXMLpath)
			EndIf
		EndIf
	EndSr
	
	BegSr CreateTbl Access(*Private)
	
		DclArray Keys    Type(DataColumn) Rank(1)

		ShipViaTbl = *New DataTable("ShipVia")

		ShipViaTbl.Columns.Add(*New DataColumn("ShipVia", *TypeOf(String)))
		ShipViaTbl.Columns["ShipVia"].ReadOnly = *True

		ShipViaTbl.Columns.Add(*New DataColumn("Description", *TypeOf(String)))
		ShipViaTbl.Columns["Description"].ReadOnly = *True
		
		ShipViaTbl.Columns.Add(*New DataColumn("ShipViaDescription", *TypeOf(String)))
		ShipViaTbl.Columns["ShipViaDescription"].Expression = "ShipVia + ' - ' + Description"
		ShipViaTbl.Columns["ShipViaDescription"].ReadOnly = *True
		
		Keys = *New DataColumn[] {ShipViaTbl.Columns("ShipVia")}
		ShipViaTbl.PrimaryKey = Keys
	EndSr

	BegSr PopulateDataTable Access(*Private)
	
		DclFld dr Type(DataRow)
		
		If Not DSship.IsOpen
			Open DSship
		EndIf
		
		DoUntil DSship.IsEof
			Read DSship
			If Not DSship.IsEof
				dr = ShipViaTbl.NewRow()
				
				dr.Item["ShipVia"]     = SHCDE
				dr.Item["Description"] = SHDSC.Trim()
				
				ShipViaTbl.Rows.Add(dr)
			EndIf
		EndDo
		
		ShipViaTbl.WriteXml(pXMLpath)
		
		Close DSship
	EndSr

	BegFunc GetShipViaTbl Type(DataTable) Access(*Public)
		
		LeaveSR ShipViaTbl
	EndFunc
EndClass