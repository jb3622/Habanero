// ===============================================
// Joseph Urbina
// 21st September 2009
// DSSPEHLcls	
// Data Access Layer - Spice History
//------------------------------------------------
// Modification
//================================================

Using System
Using System.Text
Using System.Data
Using System.Collections
Using System.IO

BegClass DSSPEHLcls Access(*Public)
	
	/Region Declares
	DclFld dtHistory Type( DataTable ) New()
	DclFld dcHistory Type( DataColumn ) 
	DclFld drHistory Type( DataRow )
	DclFld WrkDate   Type(*Date)
	Dclfld WrkTime   Type(*Time)
	/EndRegion
			
	/Region Declare Files and DB
	DclDB Name(DB) DbName("TDST")
	DclDiskFile Name(DSSPEHL) Type(*Update) Org(*Indexed) ImpOpen(*No) DB(DB) AddRec(*Yes) RnmFmt(RDSSPEHL:R_SPEHL) 
	/EndRegion
		
	/Region Constructor
	BegConstructor Access(*Public)
		DclSrParm DB Type(ASNA.VisualRPG.Runtime.Database)
				
		*this.DB   = DB
				
	EndConstructor
	
	BegConstructor Access(*Public)
				
		*this.DB = DB
	EndConstructor
			
	/EndRegion
			
	/Region DSSPSSD Properties
	//History Type
	BegProp Event_Type Type(*Char) Len(4) Access(*Public)
		BegGet
			LeaveSR EHLTYP
		EndGet
	EndProp
	
	//History Key
	BegProp Event_Key Type(*Char) Len(20) Access(*Public)
		BegGet
			LeaveSR EHLKEY
		EndGet
	EndProp
	//History Reference
	BegProp Event_Ref Type(*Zoned) Len(7,0) Access(*Public)
		BegGet
			LeaveSR EHLREF
		EndGet	
	EndProp	
	//History Description
	BegProp Event_Desc Type(*Char) Len(30) Access(*Public)
		BegGet
			LeaveSR EHLDES
		EndGet	
		
	EndProp
	
	//Create Date
	BegProp Create_Date Type(*Zoned ) Len(8,0) Access(*Public)
		BegGet
			LeaveSR EHLCRD
		EndGet
				
	EndProp
	
	//Create Time
	BegProp Create_Time Type(*Zoned ) Len(6,0) Access(*Public)
		BegGet
			LeaveSR EHLCRT
		EndGet
				
	EndProp
	
	//Create User
	BegProp Create_User Type(*Char ) len(10) Access(*Public)
		BegGet
			LeaveSR EHLCRU 
		EndGet
				
	EndProp
		
	/EndRegion	
			
	/Region Methods
		
	//Returns the whole File as a Dataset Keyed by Event Type and Event Key.
	BegFunc GetHistoryDataSet Type(DataTable) Access(*Public)
		DclSrParm  Event_Type Type(*Char) Len(4)
		DclSrParm  Event_Key  Type(*Char) Len(20)
							
		If Not DSSPEHL.IsOpen
			Open DSSPEHL
		EndIf
		
		//Create History Event Ref Column
		dcHistory = *New DataColumn( 'clmEHLREF' )
		dcHistory.DataType = System.Type.GetType("System.Int32")
		dcHistory.ColumnName = "clmEHLREF"
		dcHistory.Caption = "History Ref"
		dtHistory.Columns.Add(dcHistory)
		
		//Create Creation Date Column
		dcHistory = *New DataColumn( 'clmEHLCRD' )
		dcHistory.DataType = System.Type.GetType("System.String")
		dcHistory.ColumnName = "clmEHLCRD"
		dcHistory.Caption = "Date"
		dtHistory.Columns.Add(dcHistory)
		
		//Create Creation Time Column
		dcHistory = *New DataColumn( 'clmEHLCRT' )
		dcHistory.DataType = System.Type.GetType("System.String")
		dcHistory.ColumnName = "clmEHLCRT"
		dcHistory.Caption = "Time"
		dtHistory.Columns.Add(dcHistory)
		
		//Create Creation User Column
		dcHistory = *New DataColumn( 'clmEHLCRU' )
		dcHistory.DataType = System.Type.GetType("System.String")
		dcHistory.ColumnName = "clmEHLCRU"
		dcHistory.Caption = "User"
		dtHistory.Columns.Add(dcHistory)
		
		//Create History Event Description Column
		dcHistory = *New DataColumn( 'clmEHLDES' )
		dcHistory.DataType = System.Type.GetType("System.String")
		dcHistory.ColumnName = "clmEHLDES"
		dcHistory.Caption = "Event Description"
		dtHistory.Columns.Add(dcHistory)
		
		//Read file and create rows in DataTable				
		Setll DSSPEHL Key(Event_Type, Event_Key)
		DoUntil DSSPEHL.IsEof
			ReadE DSSPEHL Key(Event_Type, Event_Key) Access(*NoLock)
			If not DSSPEHL.IsEof
				drHistory = dtHistory.NewRow()
				drHistory.Item["clmEHLREF"] = EHLREF
				
				//Convert Date
				WrkDate = EHLCRD
				drHistory.Item["clmEHLCRD"] = WrkDate.ToString('dd - MMM - yyyy').ToUpper()
				WrkTime = EHLCRT
				drHistory.Item["clmEHLCRT"] = WrkTime.ToString('HH:mm:ss')
				drHistory.Item["clmEHLCRU"] = EHLCRU
				drHistory.Item["clmEHLDES"] = EHLDES
				dtHistory.Rows.Add(drHistory)				
			Endif
		Enddo
		
			LeaveSR dtHistory
	EndFunc
	
	//Writes new record. This method works out the next ID number.			
	BegSR WriteHistoryEvent Access(*Public)
		DclSrParm  Event_Type Type(*Char) Len(4)
		DclSrParm  Event_Key  Type(*Char) Len(20)
		DclSrParm  Event_Desc Type(*Char) Len(30)
						
		If Not DSSPEHL.IsOpen
			Open DSSPEHL
		EndIf
		
		//Get last available ID Number
		SetGT File(DSSPEHL) Key(Event_Type,Event_Key)
		ReadPE DSSPEHL      Key(Event_Type,Event_Key)
		
		If %Error = *On
			Throw *New System.Exception('Record Locked')
		EndIf
		
		//As we have previously read a record we don't want any data hanging arouond.
		clear R_SPEHL *NoKey
		
		//Increment the ID	
		If Not %Eof(DSSPEHL)
			EHLREF += 1
		  Else
		   EHLREF  = 1	
		Endif
		
		//Set new record data		
		EHLTYP = Event_Type
		EHLKEY = Event_Key 
		EHLDES = Event_Desc
		EHLCRD = %Date
	    EHLCRT = %Time
	    EHLCRU = db.User().toUpper()
		
		//Write the record
		Write R_SPEHL Err(*Extended)
		If %Error = *On
			Throw *New System.Exception('Failed to write record (DSSPEHL)')
		EndIf
		
		LeaveSR 
	EndSR
	/EndRegion
EndClass
