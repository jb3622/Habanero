Using System
Using System.Text
Using System.Data

BegClass AddItem Access(*Public)
	
	/Region Declares 
	DclFld PgmDB                  Type(ASNA.VisualRPG.Runtime.Database) Access(*Private)
	DclFld User                   Type(Disney.Menu.Users)          Access(*Private)
	DclFld Environment            Type(Disney.Menu.Environments)   Access(*Private)
	DclFld ItemsBO                Type(Disney.Spice.ItemsBO.Items) Access(*Private)	
	DclFld qNextForm              Type(System.Collections.Stack)   Access(*Private)	
	DclFld SelectMarket           Type(SelectMarket)    Access(*Private)
	DclFld AddStyleDetails        Type(AddStyleDetails) Access(*Private)
	DclFld AddSizeGroup           Type(AddSizeGroup)    Access(*Private)
	DclFld SelectSize             Type(SelectSize)      Access(*Private)
	DclFld SelectSizeBandM        Type(SelectSizeBandM) Access(*Private)
	DclFld AddMarketSize          Type(AddMarketSize)   Access(*Private)
	DclFld AddOnlineEcomDetails   Type(AddOnlineEcomDetails)   Access(*Private)	
	/EndRegion
	
	/Region Constructors

	BegConstructor Access(*Public)
		DclSrParm PgmDB       Type(ASNA.VisualRPG.Runtime.Database)
		DclSrParm User        Type(Disney.Menu.Users)
		DclSrParm Environment Type(Disney.Menu.Environments)
		DclSrParm ItemsBO     Type(Disney.Spice.ItemsBO.Items)
		
		*this.PgmDB = PgmDB
		*this.User  = User
		*this.Environment = Environment
		*this.ItemsBO     = ItemsBO
	EndConstructor
	
	BegConstructor Access(*Public)
		DclSrParm PgmDB       Type(ASNA.VisualRPG.Runtime.Database)
		DclSrParm User        Type(Disney.Menu.Users)
		DclSrParm Environment Type(Disney.Menu.Environments)
				
		*this.PgmDB = PgmDB
		*this.User  = User
		*this.Environment = Environment
	EndConstructor
	
	/EndRegion
	
	BegSR Add Access(*Public)
	
		DclFld NextForm Type(String)
			
		ItemsBO = *New Disney.Spice.ItemsBO.Items(PgmDB,User,Environment)
		
		Try
			qNextForm = *New System.Collections.Stack()
			qNextForm.Push('SelectMarkets')
			
			DoUntil NextForm = 'End'
				NextForm = qNextForm.Peek() *As String
				
				Select
				When NextForm = 'SelectMarkets'
					SelectMarkets()
				When NextForm = 'ItemDetails'
					ItemDetails()
				When NextForm = 'AddOnlineEcomDetails'
					AddOnlineEcomDetail()
				When NextForm = 'SizeGroups'
					SizeGroups()
				When NextForm = 'SizesBandM'
					SizesBandM()
				When NextForm = 'Sizes'
					Sizes()
				When NextForm = 'MarketSizes'
					MarketSizes()
				When NextForm = 'WriteItem'
					WriteItem()
				EndSL
			EndDo		
		Catch Name(exc) Type(Exception)
			//MsgBox TITLE('Error') Msg(exc.Message)  ICON(*Stop)
		EndTry
	EndSR
	
	BegSr SelectMarkets Access(*Private)
		
		DclFld MktList Type(StringBuilder)
		DclFld dv      Type(DataView)
		
		If SelectMarket = *Nothing
			SelectMarket = *New SelectMarket(User,Environment,ItemsBO)
			SelectMarket.Opacity = 1.00
		EndIf
		
		Try
			SelectMarket.SelectMarkets()
			If SelectMarket.DialogResult = System.Windows.Forms.DialogResult.Abort
				qNextForm.Push('End')
				LeaveSR
			EndIf
			
			// Generate list of selected countries
			MktList = *New StringBuilder()
			
			dv = *New DataView(ItemsBO.SpiceDataSet.Tables("DomainMarkets"))
			dv.RowFilter = "Select = true"
			ForEach TickedMarket Collection(dv) Type(DataRowView)
				MktList.Append(TickedMarket["IPMarket"] *As String)
				MktList.Append(",")
			EndFor
			
			If MktList.Length > *Zero
				MktList.Remove(MktList.Length-1,1)
				ItemsBO.SelectedMarkets = MktList.ToString()
			EndIf
			
			// Next Form
			qNextForm.Push('ItemDetails')
		Catch Name(exc) Type(Exception)
			//MsgBox TITLE('Error') Msg(exc.Message) ICON(*Stop)
		EndTry
	EndSr
	
	BegSr ItemDetails Access(*Private)
		
		//qNextForm.Pop()    // Car'nt go back from here
		AddStyleDetails = *Nothing
		AddStyleDetails = *New AddStyleDetails(PgmDB,User,Environment,ItemsBO)
				
		Try
			AddStyleDetails.ShowDialog()
					
			If AddStyleDetails.DialogResult = System.Windows.Forms.DialogResult.Abort
				qNextForm.Push('End')
				LeaveSR
			EndIf
			
			If AddStyleDetails.DialogResult = System.Windows.Forms.DialogResult.Retry
				qNextForm.Pop()
				LeaveSR
			EndIf
			 			
			If(ItemForOnlineEcom() = *True)
				qNextForm.Push('AddOnlineEcomDetails')
			Else
				qNextForm.Push('SizeGroups')
			EndIf
				
		Catch Name(exc) Type(Exception)
			//MsgBox TITLE('Error') Msg(exc.Message)  ICON(*Stop)
		EndTry
	EndSr
	
	BegSr AddOnlineEcomDetail Access(*Private)			
		
		AddOnlineEcomDetails = *Nothing
		AddOnlineEcomDetails = *New AddOnlineEcomDetails(PgmDB,User,Environment,ItemsBO)
				
		AddOnlineEcomDetails.ShowDialog()
		If AddOnlineEcomDetails.DialogResult = System.Windows.Forms.DialogResult.Abort
			qNextForm.Push('End')
			LeaveSR
		EndIf
		
		If AddOnlineEcomDetails.DialogResult = System.Windows.Forms.DialogResult.Retry
			qNextForm.Pop()
			LeaveSR
		EndIf
		
		// Next Form
		qNextForm.Push('SizeGroups')
	EndSr
	
	BegSr SizeGroups Access(*Private)
		
		DclFld dv Type(DataView)
		
		Select
		When ItemsBO.SpiceDataSet.Tables["FilteredSizeGroups"].Rows.Count = *Zero
			
			//Settings for NO sizes
			ItemsBO.GetNoSizeTbl()
			qNextForm.Pop()
			qNextForm.Push('MarketSizes')
			LeaveSR
	
		When ItemsBO.SpiceDataSet.Tables["FilteredSizeGroups"].Rows.Count = 1		
			
			// Only one size group, so automatically select it
			dv = *New DataView(ItemsBO.SpiceDataSet.Tables["FilteredSizeGroups"])
			ForEach dvr Collection(dv) Type(DataRowView)
				dvr["Select"] = *True
			EndFor
			
			qNextForm.Pop()
			
			If(ItemForOnlineEcom() = *True)
				qNextForm.Push('Sizes')
			Else
				qNextForm.Push('SizesBandM')
			EndIf			
					
			LeaveSR
		
		Other
			// Show SizeGroup form for selection of Groups
			If AddSizeGroup = *Nothing
				AddSizeGroup = *New AddSizeGroup(PgmDB,ItemsBO)
			EndIf
			AddSizeGroup.ShowDialog()
			
			If AddSizeGroup.DialogResult = System.Windows.Forms.DialogResult.Abort
				qNextForm.Push('End')
				LeaveSR
			EndIf
			
			If AddSizeGroup.DialogResult = System.Windows.Forms.DialogResult.Retry
				qNextForm.Pop()
				LeaveSR
			EndIf
				
			If(ItemForOnlineEcom() = *True)
				qNextForm.Push('Sizes')
			Else
				qNextForm.Push('SizesBandM')
			EndIf	
			
			LeaveSR
		EndSL
	EndSr
	
	BegSr Sizes Access(*Private)

		SelectSize = *Nothing
		SelectSize = *New SelectSize(PgmDB,ItemsBO)
				
		SelectSize.ShowDialog()
		
		If SelectSize.DialogResult = System.Windows.Forms.DialogResult.Abort
			qNextForm.Push('End')
			LeaveSR
		EndIf
				
		If SelectSize.DialogResult = System.Windows.Forms.DialogResult.Retry
			qNextForm.Pop()
			LeaveSR
		EndIf
		
		qNextForm.Push('MarketSizes')
	EndSr
	
	BegSr SizesBandM Access(*Private)
						
		SelectSizeBandM = *Nothing
		SelectSizeBandM = *New SelectSizeBandM(PgmDB,ItemsBO)
				
		SelectSizeBandM.ShowDialog()
		
		If SelectSizeBandM.DialogResult = System.Windows.Forms.DialogResult.Abort
			qNextForm.Push('End')
			LeaveSR
		EndIf
				
		If SelectSizeBandM.DialogResult = System.Windows.Forms.DialogResult.Retry
			qNextForm.Pop()
			LeaveSR
		EndIf
		
		qNextForm.Push('MarketSizes')
	EndSr
		
	BegSr MarketSizes Access(*Private)
			
		If AddMarketSize = *Nothing
			AddMarketSize = *New AddMarketSize(PgmDB,ItemsBO,Environment)
		EndIf
				
		AddMarketSize.ShowDialog()
		
		If AddMarketSize.DialogResult = System.Windows.Forms.DialogResult.Abort
			qNextForm.Push('End')
			LeaveSR
		EndIf
		
		If AddMarketSize.DialogResult = System.Windows.Forms.DialogResult.Retry
			qNextForm.Pop()
			LeaveSR
		EndIf
		
		qNextForm.Push('WriteItem')
	EndSr
	
	BegSr WriteItem Access(*Private)
		
		DclFld ReturnValue Type(Boolean)
		
		ReturnValue = ItemsBO.WriteItem()
		
		If ReturnValue = *True
			MsgBox 'Item has been written' Title("Success") Icon(*Information)
		Else
			MsgBox 'Item has NOT been written' Icon(*Exclamation)
		EndIf
		
		qNextForm.Push('End')
	EndSR

	BegFunc ItemForOnlineEcom Type(Boolean) Access(*Private) 		
		DclFld result Type(Boolean) Inz(*False)		
		DclFld dataRow Type(DataRow)
		DclFld forOnlineEcom Type(String)
		
		Try
			dataRow = ItemsBO.SpiceDataSet.Tables["SellingChannelTable"].Rows.Find(ItemsBO.SellingChannel.ToString())
			If (dataRow <> *Nothing)		
				forOnlineEcom = dataRow["OnlineECOM"] *AS String				
				If (forOnlineEcom <> *Nothing)
					If (forOnlineEcom.Trim() = "Y")
						result = *True
					Else
						result = *False
					EndIf
				EndIf
			EndIf
		Catch Name(exc) Type(Exception)
			//MsgBox TITLE('Error') Msg(exc.Message) ICON(*Stop)
		EndTry		
		LeaveSR result
	EndFunc
	
EndClass
